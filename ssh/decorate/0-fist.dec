actor "Fists" : Weapon
{
    Weapon.SlotNumber 0
    Weapon.KickBack 50
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    Obituary "%k shattered %o's skull."
    DamageType "ShihongPunch"
    Weapon.UpSound "weapon/switch"
    +NOALERT
    +NOAUTOFIRE
    +ALT_AMMO_OPTIONAL
    +NOEXTREMEDEATH
    +CHEATNOTWEAPON
    +UNDROPPABLE
    States
    {
    Ready:
        SHF0 A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SwapToHaggarMode",1,"StyleSwap")
        TNT1 A 0 A_JumpIfInventory("SwapToRangedMode",1,"StyleSwap")
        TNT1 A 0 A_JumpIfInventory("SwapToTricksterMode",1,"StyleSwap")
        Loop
    Select:
        TNT1 AAAA 0 A_Raise
        SHF0 A 1 A_Raise
        loop
    Deselect:
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 AAAA 0 A_Lower
        SHF0 A 1 A_Lower
        Loop
    StyleSwap:
        TNT1 A 0 A_GiveInventory("StyleSwapping",1)
        SHF0 BCDD 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("styleswap/open","soundslot5",0,0)
        TNT1 A 30
        TNT1 A 0 A_PlaySoundEx("styleswap/insert","soundslot6",0,0)
        TNT1 A 25
        TNT1 A 0 A_PlaySoundEx("styleswap/transform","soundslot5",0,0)
        TNT1 A 0 A_SetBlend("white",1.0,105)
        TNT1 A 0 A_JumpIfInventory("SwapToHaggarMode",1,"StyleSwapHaggarFinish")
        TNT1 A 0 A_JumpIfInventory("SwapToRangedMode",1,"StyleSwapRangedFinish")
        TNT1 A 0 A_JumpIfInventory("SwapToTricksterMode",1,"StyleSwapTricksterFinish")
        TNT1 A 5 // Failsafe in case it doesn't work.
        TNT1 A 0 A_TakeInventory("SwapToHaggarMode",1)
        TNT1 A 0 A_TakeInventory("SwapToRangedMode",1)
        TNT1 A 0 A_TakeInventory("SwapToTricksterMode",1)
        goto Ready
    StyleSwapHaggarFinish:
        TNT1 A 0 ACS_ExecuteAlways(362,0,2,0,0)
        TNT1 A 0 A_GiveInventory("HaggarModeOn",1)
        TNT1 A 0 A_TakeInventory("RangedModeOn",1)
        TNT1 A 0 A_TakeInventory("TricksterModeOn",1)
        TNT1 A 0 A_TakeInventory("SwapToHaggarMode",1)
        TNT1 A 0 A_TakeInventory("SwapToRangedMode",1)
        TNT1 A 0 A_TakeInventory("SwapToTricksterMode",1)
        TNT1 A 0 A_TakeInventory("StyleSwapping",1)
        TNT1 A 0 A_GiveInventory("SwapCooldown",210)
        goto StyleSwapFinish
    StyleSwapRangedFinish:
        TNT1 A 0 ACS_ExecuteAlways(362,0,2,1,0)
        TNT1 A 0 A_TakeInventory("HaggarModeOn",1)
        TNT1 A 0 A_GiveInventory("RangedModeOn",1)
        TNT1 A 0 A_TakeInventory("TricksterModeOn",1)
        TNT1 A 0 A_TakeInventory("SwapToHaggarMode",1)
        TNT1 A 0 A_TakeInventory("SwapToRangedMode",1)
        TNT1 A 0 A_TakeInventory("SwapToTricksterMode",1)
        TNT1 A 0 A_TakeInventory("StyleSwapping",1)
        TNT1 A 0 A_GiveInventory("SwapCooldown",210)
        goto StyleSwapFinish
    StyleSwapTricksterFinish:
        TNT1 A 0 ACS_ExecuteAlways(362,0,2,2,0)
        TNT1 A 0 A_TakeInventory("HaggarModeOn",1)
        TNT1 A 0 A_TakeInventory("RangedModeOn",1)
        TNT1 A 0 A_GiveInventory("TricksterModeOn",1)
        TNT1 A 0 A_TakeInventory("SwapToHaggarMode",1)
        TNT1 A 0 A_TakeInventory("SwapToRangedMode",1)
        TNT1 A 0 A_TakeInventory("SwapToTricksterMode",1)
        TNT1 A 0 A_TakeInventory("StyleSwapping",1)
        TNT1 A 0 A_GiveInventory("SwapCooldown",210)
        goto StyleSwapFinish
    StyleSwapFinish:
        TNT1 A 5
        SHF0 DDCBA 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        Goto Ready

    Fire:
        TNT1 A 0 A_JumpIfInventory("Grappling",1,"CancelGrapple")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"FirePrep")
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",4,"Punch5")
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",3,"Punch4")
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",2,"Punch3")
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",1,"Punch2")
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"Punch1")
        SHF0 BCD 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 1 A_GiveInventory("SlashingLikeAGaijin",1)
        goto Punch1
    FirePrep:
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",4,"Punch5")
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",3,"Punch4")
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",2,"Punch3")
        TNT1 A 0 A_JumpIfInventory("PunchComboCounter",1,"Punch2")
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
    Punch1:
        TNT1 A 0 A_TakeInventory("PunchComboCounter",4)
        TNT1 A 0 A_PlayWeaponSound("punchy/swing1")
        SHF1 AB 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) // PUNCH PROJECTILES OUT OF THE AIR
        SHF1 C 1 A_FireCustomMissile("PunchAttack1",0,0,0,0,2) // Give it some autoaim buffs.
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF1 D 1
        SHF1 E 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOSECONDARY)
        goto Fired
    Punch2:
        TNT1 A 0 A_TakeInventory("PunchComboCounter",4)
        TNT1 A 0 A_PlayWeaponSound("punchy/swing1")
        SHF1 AB 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF1 C 1 A_FireCustomMissile("PunchAttack2",0,0,0,0,2)
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF1 D 1
        SHF1 E 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOSECONDARY)
        goto Fired
    Punch3:
        TNT1 A 0 A_TakeInventory("PunchComboCounter",4)
        TNT1 A 0 A_PlayWeaponSound("punchy/swing2")
        SHF2 AB 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF2 C 1 A_FireCustomMissile("PunchAttack3",0,0,0,0,2)
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF2 DE 1
        SHF2 F 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOSECONDARY)
        goto Fired
    Punch4:
        TNT1 A 0 A_TakeInventory("PunchComboCounter",4)
        TNT1 A 0 A_PlayWeaponSound("punchy/swing3")
        SHF3 AB 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF3 C 1 A_FireCustomMissile("PunchAttack4",0,0,0,0,2)
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF3 DE 1
        SHF3 F 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOSECONDARY)
        goto Fired
    Punch5:
        TNT1 A 0 A_TakeInventory("PunchComboCounter",4)
        TNT1 A 0 A_PlayWeaponSound("punchy/swing4")
        SHF4 ABC 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF4 D 1 A_FireCustomMissile("PunchAttack5",0,0,0,0,2)
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        SHF4 E 3
        SHF4 FG 1
        SHF4 H 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOSECONDARY)
        goto Fired3

    Fired:
        TNT1 A 10 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
    Fired2:
        //TNT1 A 0 A_JumpIfInventory("SwordAttack",6,"HoldAttack")
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 50 A_WeaponReady//(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("PunchComboCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        SHF0 DCB 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        Goto Ready
    Fired3:
        //TNT1 A 0 A_JumpIfInventory("SwordAttack",6,"HoldAttack")
        TNT1 A 10 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        SHF0 DCB 1 A_WeaponReady(WRF_NOSWITCH)
        Goto Ready

    Hold:
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("DoubleTapLeft",1,"SideSpecialLeft")
        TNT1 A 0 A_JumpIfInventory("DoubleTapRight",1,"SideSpecialRight")
        TNT1 A 0 A_JumpIfInventory("DoubleTapForward",1,"ForwardSpecial")
        TNT1 A 0 A_JumpIfInventory("DoubleTapBack",1,"BackSpecial")
        TNT1 A 0 A_JumpIfInventory("SynthAltFire",1,"GunSpecial")
        TNT1 A 0 A_Refire
        TNT1 A 0 A_ClearRefire
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        goto Fired2

    ForwardSpecial:
        TNT1 A 0 A_JumpIfInventory("HaggarModeOn",1,"SuperUppercut")
        TNT1 A 0 A_JumpIfInventory("RangedModeOn",1,"ConcussionMissile")
        TNT1 A 0 A_JumpIfInventory("TricksterModeOn",1,"SpecialAttackFinish")
        TNT1 A 1
        loop
    SuperUppercut:
        TNT1 A 0 A_UnsetShootable
        TNT1 A 0 A_CheckFloor("UppercutOnFloor")
        TNT1 A 0 A_Recoil(-25)
        TNT1 A 0 ThrustThingZ(0,5,0,0)
        goto UppercutReady
    UppercutOnFloor:
        TNT1 A 0 A_Recoil(-35)
        TNT1 A 0 ThrustThingZ(0,85,1,1)
    UppercutReady:
        FSP3 ABCD 1
        FSP3 EE 2
        FSP3 DCBA 1
        TNT1 A 0
        TNT1 A 0 A_SetShootable
        TNT1 A 0 A_PlaySoundEx("iron/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SetPitch(pitch-1)
        SHF4 A 1
        TNT1 A 0 A_SetPitch(pitch-2)
        TNT1 A 0 ThrustThingZ(0,40,0,0)
        SHF4 BC 1
        TNT1 A 0 A_SetPitch(pitch-4)
        TNT1 A 0
        SHF4 D 1 A_SpawnItemEx("DashUppercutExplosion",96,0,0,0,0,0,0,48)
        SHF4 E 4
        TNT1 A 0 A_SetPitch(pitch+4)
        SHF4 F 2
        SHF4 G 1
        TNT1 A 0 A_SetPitch(pitch+2)
        SHF4 H 1
        TNT1 A 0 A_SetPitch(pitch+1)
        TNT1 A 1
        goto SpecialAttackFinish
    ConcussionMissile:
        TNT1 A 0 A_PlayWeaponSound("combo/ready")
        SHF1 AB 1
        SHF1 C 15
        TNT1 A 0 A_PlayWeaponSound("supermissile/fire")
        TNT1 A 0 A_FireCustomMissile("ConcussionMissile",0,0)
        SHF1 D 1
        SHF1 E 1
        TNT1 A 10
        goto SpecialAttackFinish

    SideSpecialLeft:
        TNT1 A 0 A_JumpIfInventory("HaggarModeOn",1,"SpinAttackLeft")
        TNT1 A 0 A_JumpIfInventory("RangedModeOn",1,"HomingMissilesLaunch")
        TNT1 A 0 A_JumpIfInventory("TricksterModeOn",1,"SpecialAttackFinish")
        TNT1 A 1
        loop
    SpinAttackLeft:
        TNT1 A 0
        TNT1 A 1 A_SetAngle(angle+11.25)
        TNT1 A 1 A_SetAngle(angle+22.5)
        //TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("iron/spinattack")
        //TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_SpawnItemEx("LariatExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackLeftContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackLeftContinue:
        FSP2 A 1 A_SetAngle(angle+45)
        FSP2 B 1 A_SetAngle(angle+50)
        FSP2 C 1 A_SetAngle(angle+55)
        FSP2 D 1 A_SetAngle(angle+55)
        FSP2 E 1 A_SetAngle(angle+50)
        FSP2 F 1 A_SetAngle(angle+45)
        FSP2 G 1 A_SetAngle(angle+22.5)
        TNT1 A 1 A_SetAngle(angle+11.25)
        goto SpecialAttackFinish
    HomingMissilesLaunch:
        TNT1 A 0 A_PlayWeaponSound("combo/ready")
        SHF1 AB 1
        SHF1 C 15
        TNT1 A 0 A_PlayWeaponSound("supermissile/fire")
        TNT1 AAAAAAA 0 A_FireCustomMissile("HomingMissile",random(-900,-1100)/10.00,0,0,16,0,random(-10,10))
        TNT1 AAAAAAA 0 A_FireCustomMissile("HomingMissile",random(900,1100)/10.00,0,0,16,0,random(-10,10))
        SHF1 D 1
        SHF1 E 1
        TNT1 A 10
        goto SpecialAttackFinish

    SideSpecialRight:
        TNT1 A 0 A_JumpIfInventory("HaggarModeOn",1,"SpinAttackRight")
        TNT1 A 0 A_JumpIfInventory("RangedModeOn",1,"HomingMissilesLaunch")
        TNT1 A 0 A_JumpIfInventory("TricksterModeOn",1,"SpecialAttackFinish")
        TNT1 A 1
        loop
    SpinAttackRight:
        TNT1 A 0
        TNT1 A 1 A_SetAngle(angle-11.25)
        TNT1 A 1 A_SetAngle(angle-22.5)
        //TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("iron/spinattack")
        //TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_SpawnItemEx("LariatExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackRightContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackRightContinue:
        FSP1 A 1 A_SetAngle(angle-45)
        FSP1 B 1 A_SetAngle(angle-50)
        FSP1 C 1 A_SetAngle(angle-55)
        FSP1 D 1 A_SetAngle(angle-55)
        FSP1 E 1 A_SetAngle(angle-50)
        FSP1 F 1 A_SetAngle(angle-45)
        FSP1 G 1 A_SetAngle(angle-22.5)
        TNT1 A 1 A_SetAngle(angle-11.25)
        goto SpecialAttackFinish

    BackSpecial:
        TNT1 A 0 A_JumpIfInventory("HaggarModeOn",1,"SpecialAttackFinish")
        TNT1 A 0 A_JumpIfInventory("RangedModeOn",1,"SpecialAttackFinish")
        TNT1 A 0 A_JumpIfInventory("TricksterModeOn",1,"EngageCloakingMode")
        TNT1 A 1
        loop
    EngageCloakingMode:
        TNT1 A 0 A_PlayWeaponSound("combo/ready")
        SHF1 AB 1
        SHF1 C 15
        TNT1 A 0 A_PlayWeaponSound("stealth/engage")
        TNT1 A 0 A_SetBlend("White",1,35)
        TNT1 AA 0 A_GiveInventory("ShadowCloak",1)
        SHF1 D 1
        SHF1 E 1
        TNT1 A 10
        goto SpecialAttackFinish

    GunSpecial:
        TNT1 A 0 A_JumpIfInventory("HaggarModeOn",1,"SpecialAttackFinish")
        TNT1 A 0 A_JumpIfInventory("RangedModeOn",1,"SpecialAttackFinish")
        TNT1 A 0 A_JumpIfInventory("TricksterModeOn",1,"GrappleBeam")
        TNT1 A 1
        loop
    GrappleBeam:
        //TNT1 A 0 A_JumpIfInventory("SuperMeterCounter",10,1)
        //goto SpecialAttackFinish
        TNT1 A 0
        TNT1 A 0 A_ClearRefire
        TNT1 A 2 A_FireBullets(0, 0, 1, 0, "GrapplePuff", 0, 1540)//1028)
        TNT1 AA 1 A_JumpIfInventory("GrappleHit",1,"GrappleSound")
        TNT1 A 15 A_PlayWeaponSound("claw/firemiss")
        TNT1 A 0 A_JumpIfInventory("SynthAltFire",1,"GunSpecial")
        TNT1 A 0 A_TakeInventory("PunchComboCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        SHF0 DCB 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        goto Ready
    GrappleSound:
        TNT1 A 0 A_PlayWeaponSound("claw/firehit")
        TNT1 A 0 ACS_ExecuteAlways(655, 0, ACS_ExecuteWithResult(657, 1), 2, 0)
        TNT1 A 0 A_TakeInventory("GrappleTIDIndicator",1)
        TNT1 A 0 A_TakeInventory("GrappleHit",1)
        //TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_GiveInventory("Grappling",1)
        TNT1 A 1
        TNT1 A 0 A_TakeInventory("PunchComboCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        SHF0 DCB 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        goto Ready
    CancelGrapple:
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("Grappling",1)
        TNT1 A 20 A_PlaySoundEx("claw/fireend","soundslot6",0,0)
        goto Ready

    SpecialAttackFinish:
        TNT1 A 0 A_TakeInventory("PunchComboCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_TakeInventory("DoubleTapBack", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapLeft", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapRight", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapForward", 1)
        TNT1 A 0 A_ClearRefire
        goto Fired3

    AltFire:
        TNT1 A 0 //A_TakeInventory("PointsNoAttacks",1)
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"AltFirePrepare")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"AltFireGun")
        SHF0 BCD 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
        goto AltFireGun
    AltFirePrepare:
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 //A_TakeInventory("PunchComboCounter",4)
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
    AltFireGun:
		SHTS AB 1
		TNT1 A 0 A_PlayWeaponSound("taser/attack")
        TNT1 A 0 A_SetBlend("white",0.2,3)
		SHTS C 1 BRIGHT A_CustomPunch(35,1,0,"ElecPuff",128)
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
		SHTS DE 1 BRIGHT
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
		SHTS FG 1 BRIGHT
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
		SHTS HI 1 BRIGHT
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
		SHTS J 1 BRIGHT
		SHTS KK 1
		SHTS LLM 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOBOB)
        TNT1 A 70 A_WeaponReady
		Goto AltFired
    AltFired:
        TNT1 A 0 A_TakeInventory("PunchComboCounter",4)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        SHF0 DCB 1 A_WeaponReady
        Goto Ready
    }
}

actor PunchAttack1 : FastProjectile
{
    Radius 6
    Height 6
    Damage (50)
    Speed 96
    RenderStyle Add
    Alpha 0.6
    Scale 0.65
    SeeSound "none"
    DeathSound "none"
    Species "Player"
    DamageType "ShihongPunch"
    Projectile
    +BLOODSPLATTER
    +THRUSPECIES
    +THRUGHOST
    +NOEXTREMEDEATH
    //+FORCEPAIN - Nope. As much as I was looking forward to this flag, stunlocking a Cyber is a no-go.
    States
    {
    Spawn:
        TNT1 A 1
        stop
    Death:
    Crash:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_TakeFromTarget("PunchComboCounter",4)
        TNT1 A 0 A_PlaySound("punchy/wall1")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake",0,0,0,0,0,0,0,32)
        TNT1 AA 0 A_SpawnItemEx("IronMaidenImpactSmoke",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        TNT1 AA 0 A_SpawnItemEx("HammerSmoke",0,0,0,random(-150,150)/150.00,random(-150,150)/150.00,random(-150,150)/150.00,0,160)
        FHFX SS 1
        FHFX TU 2
        FHFX VW 3
        stop
    XDeath:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("PunchComboCounter",1)
        TNT1 A 0 A_GiveToTarget("KharonSlashPoints",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",3)
        TNT1 A 0 A_PlaySound("punchy/hit1")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake",0,0,0,0,0,0,0,32)
        FHFX SS 1  
        FHFX TU 2
        FHFX VW 3
        stop
    }
}

actor PunchAttack2 : PunchAttack1
{
    States
    {
    XDeath:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("PunchComboCounter",2)
        TNT1 A 0 A_GiveToTarget("KharonSlashPoints",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",3)
        TNT1 A 0 A_PlaySound("punchy/hit1")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake",0,0,0,0,0,0,0,32)
        FHFX SS 1  
        FHFX TU 2
        FHFX VW 3
        stop
   }
}

actor PunchAttack3 : PunchAttack1
{
    Damage (70)
    States
    {
    Death:
    Crash:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_TakeFromTarget("PunchComboCounter",4)
        TNT1 A 0 A_PlaySound("punchy/wall2")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake2",0,0,0,0,0,0,0,32)
        TNT1 AA 0 A_SpawnItemEx("IronMaidenImpactSmoke",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        TNT1 AA 0 A_SpawnItemEx("HammerSmoke",0,0,0,random(-150,150)/150.00,random(-150,150)/150.00,random(-150,150)/150.00,0,160)
        FHFX SS 1
        FHFX TU 2
        FHFX VW 3
        stop
    XDeath:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("PunchComboCounter",3)
        TNT1 A 0 A_GiveToTarget("KharonSlashPoints",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",3)
        TNT1 A 0 A_PlaySound("punchy/hit2")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake2",0,0,0,0,0,0,0,32)
        FHFX SS 1  
        FHFX TU 2
        FHFX VW 3
        stop
   }
}

actor PunchAttack4 : PunchAttack1
{
    Damage (90)
    States
    {
    Death:
    Crash:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_TakeFromTarget("PunchComboCounter",4)
        TNT1 A 0 A_PlaySound("punchy/wall3")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake2",0,0,0,0,0,0,0,32)
        TNT1 AA 0 A_SpawnItemEx("IronMaidenImpactSmoke",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        TNT1 AA 0 A_SpawnItemEx("HammerSmoke",0,0,0,random(-150,150)/150.00,random(-150,150)/150.00,random(-150,150)/150.00,0,160)
        FHFX SS 1
        FHFX TU 2
        FHFX VW 3
        stop
    XDeath:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("PunchComboCounter",4)
        TNT1 A 0 A_GiveToTarget("KharonSlashPoints",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",4)
        TNT1 A 0 A_PlaySound("punchy/hit3")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake2",0,0,0,0,0,0,0,32)
        FHFX SS 1  
        FHFX TU 2
        FHFX VW 3
        stop
   }
}

actor PunchAttack5 : PunchAttack1
{
    Damage (150)
    States
    {
    Death:
    Crash:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_TakeFromTarget("PunchComboCounter",4)
        TNT1 A 0 A_PlaySound("punchy/wall4")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake3",0,0,0,0,0,0,0,32)
        TNT1 AA 0 A_SpawnItemEx("IronMaidenImpactSmoke",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        TNT1 AA 0 A_SpawnItemEx("HammerSmoke",0,0,0,random(-150,150)/150.00,random(-150,150)/150.00,random(-150,150)/150.00,0,160)
        FHFX SS 1
        FHFX TU 2
        FHFX VW 3
        stop
    XDeath:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_TakeFromTarget("PunchComboCounter",4)
        TNT1 A 0 A_GiveToTarget("KharonSlashPoints",5)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",6)
        TNT1 A 0 A_PlaySound("punchy/hit4")
        TNT1 A 0 A_SpawnItemEx("PunchWorldQuake3",0,0,0,0,0,0,0,32)
        FHFX SS 1  
        FHFX TU 2
        FHFX VW 3
        stop
    }
}

actor PunchWorldQuake
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    -SOLID
    +NOCLIP
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 Radius_Quake(1,3,0,16,0)
        TNT1 A 1
        stop
    }
}

actor PunchWorldQuake2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    -SOLID
    +NOCLIP
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 Radius_Quake(2,5,0,16,0)
        TNT1 A 1
        stop
    }
}

actor PunchWorldQuake3
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    -SOLID
    +NOCLIP
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 Radius_Quake(3,6,0,16,0)
        TNT1 A 1
        stop
    }
}

ACTOR ElecPuff
{ 
    AttackSound "taser/wall"
    SeeSound "taser/hit"
    Scale 0.9
    RenderStyle Add
    Alpha 0.9
    DamageType Taser
    +NOGRAVITY
    +NOEXTREMEDEATH
    +PUFFONACTORS
    +FORCEPAIN
    +ALWAYSPUFF
    +PUFFGETSOWNER
    States
    {
    Spawn:
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark2",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark3",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
   	    TZOT ABCD 3 Bright
        Stop
	Melee:
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark2",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark3",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
   	    TZOT ABCD 3 Bright
        Stop
	Crash:
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark2",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
        TNT1 AAAAA 0 A_SpawnItemEx("ElecSpark3",random(-5,5),random(-5,5),0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,random(-180,180),128,0)
   	    TZOT ABCD 3 Bright
        Stop
   }
}