actor SpecialistMagazine : Ammo 
{
    Inventory.PickupMessage "Don't think you should've gotten this."
    Inventory.Amount 1
    Inventory.MaxAmount 20
    Ammo.BackpackAmount 0
    Ammo.BackpackMaxAmount 20
    +IGNORESKILL
    Inventory.Icon "ASMGA0"
    States
    {
    Spawn:
        NOPE A 1
        stop
    }
}

// DW Specialist Custom sprites by torridGristle.

actor "Thrasher + 2x DW Specialist Custom" : Weapon
{
    Weapon.SlotNumber 4
    Weapon.KickBack 50
    Weapon.AmmoUse 0
    Weapon.AmmoUse2 1
    Weapon.AmmoGive 0
    Weapon.AmmoGive2 20
    Weapon.AmmoType ""
    Weapon.AmmoType2 "SpecialistMagazine"
    Obituary "%k tore %o apart."
    DamageType "kubiuti"
    Weapon.UpSound "weapon/switch"
    Decal BulletChip
    +AXEBLOOD
    +NOALERT
    +NOAUTOFIRE
    +NOEXTREMEDEATH
    +ALT_AMMO_OPTIONAL
    +CHEATNOTWEAPON
    +UNDROPPABLE
    States
    {
    Ready:
        W2PD A 1 A_WeaponReady
        Loop
    Select:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop
    Deselect:
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        Loop

    Fire:
        W2PD A 10 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        goto Ready

    AltFire:
        TNT1 A 0 A_JumpIfNoAmmo("Nope")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"AltFireGun2")
    AltFireGun:
        W2PD B 1 BRIGHT A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_Light1
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlayWeaponSound("special/fire")
        TNT1 A 0 A_FireBullets(0,0,1,0,"BlankPuff", FBF_NORANDOM )
        TNT1 A 0 A_FireCustomMissile("UziBullet1",random(-325,325)/100.00,1,0,1,0,random(-200,200)/100.00)
        TNT1 A 0 A_Recoil(0.1)
        TNT1 A 0 A_JumpIfInventory("IAmASillyPersonWhoDoesntLikeRecoil",1,"NoRecoil")
        TNT1 A 0 A_SetAngle(angle+(random(-300,300)/1000.000))
        TNT1 A 0 A_SetPitch(pitch-(random(200,450)/1000.000))
    NoRecoil:
        W2PD C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_Light0
        W2PD DEEFFA 1 A_WeaponReady(WRF_NOSWITCH)
        W2PD A 70 A_WeaponReady
        Goto AltFired
    AltFireGun2:
        W2PD G 1 BRIGHT A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_Light1
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlayWeaponSound("special/fire")
        TNT1 A 0 A_FireBullets(0,0,1,0,"BlankPuff", FBF_NORANDOM )
        TNT1 A 0 A_FireCustomMissile("UziBullet1",random(-325,325)/100.00,1,0,1,0,random(-200,200)/100.00)
        TNT1 A 0 A_Recoil(0.1)
        TNT1 A 0 A_JumpIfInventory("IAmASillyPersonWhoDoesntLikeRecoil",1,"NoRecoil2")
        TNT1 A 0 A_SetAngle(angle+(random(-300,300)/1000.000))
        TNT1 A 0 A_SetPitch(pitch-(random(200,450)/1000.000))
    NoRecoil2:
        W2PD H 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_Light0
        W2PD IJJKKA 1 A_WeaponReady(WRF_NOSWITCH)
        W2PD A 70 A_WeaponReady
        Goto AltFired
    Nope:
        TNT1 A 0 A_PlaySoundEx("pistol/dry","soundslot7",0,0)
        W2PD A 7 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_PlaySoundEx("special/unload","soundslot7",0,0)
        W2PD LMNO 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 6
        TNT1 A 0 A_GiveInventory("SpecialistMagazine",20)
        TNT1 A 0 A_PlaySoundEx("special/load","soundslot7",0,0)
        TNT1 A 1
        W2PD ONMLA 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        Goto AltFired
    AltFired:
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        //TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        W2PD A 1
        Goto Ready
    }
}