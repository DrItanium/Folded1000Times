actor IronMaidenArmor : ArmorBonus
{
    Armor.SavePercent 100
    Armor.MaxSaveAmount 100
    DamageFactor "Normal", 0.5
    DamageFactor "ArchvileFire", 0.25
    DamageFactor "HeavyEnemyDamage", 0.25
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "EnemyInfightDamageCyber", 0.0
    DamageFactor "Lava", 0.0
    DamageFactor "Fire", 0.0
    DamageFactor "Slime", 0.0
    DamageFactor "Drowning", 0.0
}

actor IronMaidenArmor2 : BasicArmorPickup
{
    Armor.SavePercent 100
    Armor.SaveAmount 100
    DamageFactor "Normal", 0.5
    DamageFactor "ArchvileFire", 0.25
    DamageFactor "HeavyEnemyDamage", 0.25
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "EnemyInfightDamageCyber", 0.0
    DamageFactor "Lava", 0.0
    DamageFactor "Fire", 0.0
    DamageFactor "Slime", 0.0
    DamageFactor "Drowning", 0.0
}

ACTOR IronMaidenRepel
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    +BLOODLESSIMPACT
    -SOLID
    DamageType "ExodusJump"
    Obituary "%k shouldn't have killed %o with this."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(512,512,0)
        Stop
    }
}

actor "Iron Fist" : Weapon
{
    Weapon.KickBack 75
    Weapon.AmmoUse 0
    Weapon.AmmoUse2 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    Weapon.AmmoType2 ""
    Weapon.UpSound "henshin/ready"
    Weapon.SlotNumber 0
    +NOALERT
    +NOAUTOFIRE
    +EXTREMEDEATH
    +ALT_AMMO_OPTIONAL
    +CHEATNOTWEAPON
    States
    {
    Spawn:
        CMCP A -1
        Stop
    Ready:
        IRNF A 1 A_WeaponReady
        Loop
    Select:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        IRNF DCBB 1
        TNT1 A 0 A_Raise
        goto Ready
    Select2:
        IRNF DCBB 1 A_WeaponReady
        goto Ready
    Deselect:
        IRNF BCD 1
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        Loop

    Fire:
        //TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"FirePrepare")
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"FireSequence1")
        IRNF BCD 1
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_Jump(128,"FireSequence2")
        goto FireSequence1

    FireSequence1: // Right to left
        TNT1 A 0 A_JumpIfInventory("FistSwung",1,"FireSequence2")
        TNT1 A 0 A_Jump(255,"FireSequence1A","FireSequence1B")
        TNT1 A 0
        TNT1 A 1
        loop
    FireSequence1A:
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEX("iron/swing","SoundSlot7")
        IRP1 A 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("IronFistAttack1",0,0)
        IRP1 B 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        IRP1 C 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_JumpIfInventory("FistHit",1,"FireHit1")
        TNT1 A 0 A_SetAngle(angle-1.25)
        IRP1 D 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_SetAngle(angle-1.25)
        IRP1 E 1
        IRP1 F 1 A_GiveInventory("FistSwung",1)
        TNT1 A 6
        goto Fired
    FireHit1:
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE2,0,0)
        IRP1 GHI 1 Radius_Quake(4,10,0,16,0)
        IRP1 J 1 A_Recoil(3)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE2,0,0)
        IRP1 K 1
        IRP1 L 1 A_GiveInventory("FistSwung",1)
        TNT1 A 0 A_TakeInventory("FistHit",1)
        TNT1 A 6
        goto Fired
    FireSequence1B:
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEX("iron/swing","SoundSlot7")
        IRP3 A 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("IronFistAttack1",0,0)
        IRP3 B 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        IRP3 C 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_JumpIfInventory("FistHit",1,"FireHit1B")
        TNT1 A 0 A_SetAngle(angle-1.25)
        IRP3 D 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_SetAngle(angle-1.25)
        IRP3 E 1
        IRP3 F 1 A_GiveInventory("FistSwung",1)
        TNT1 A 6
        goto Fired
    FireHit1B:
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE2,0,0)
        IRP3 GHI 1 Radius_Quake(4,10,0,16,0)
        IRP3 J 1 A_Recoil(3)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE2,0,0)
        IRP3 K 1
        IRP3 L 1 A_GiveInventory("FistSwung",1)
        TNT1 A 0 A_TakeInventory("FistHit",1)
        TNT1 A 6
        goto Fired

    FireSequence2: // Left to right
        TNT1 A 0 A_Jump(255,"FireSequence2A","FireSequence2B")
        TNT1 A 0
        TNT1 A 1
        loop
    FireSequence2A:
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEX("iron/swing","SoundSlot7")
        IRP2 A 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("IronFistAttack1",0,0)
        IRP2 B 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        IRP2 C 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_JumpIfInventory("FistHit",1,"FireHit2")
        TNT1 A 0 A_SetAngle(angle+1.25)
        IRP2 D 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_SetAngle(angle+1.25)
        IRP2 E 1
        IRP2 F 1 A_TakeInventory("FistSwung",1)
        TNT1 A 6
        goto Fired
    FireHit2:
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE2,0,0)
        IRP2 GHI 1 Radius_Quake(4,10,0,16,0)
        IRP2 J 1 A_Recoil(3)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE2,0,0)
        IRP2 K 1
        IRP2 L 1 A_TakeInventory("FistSwung",1)
        TNT1 A 0 A_TakeInventory("FistHit",1)
        TNT1 A 6
        goto Fired
    FireSequence2B:
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEX("iron/swing","SoundSlot7")
        IRP4 A 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("IronFistAttack1",0,0)
        IRP4 B 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        IRP4 C 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_JumpIfInventory("FistHit",1,"FireHit2B")
        TNT1 A 0 A_SetAngle(angle+1.25)
        IRP4 D 1
        TNT1 A 0 A_FireCustomMissile("IronFistGuard",0,0) 
        TNT1 A 0 A_SetAngle(angle+1.25)
        IRP4 E 1
        IRP4 F 1 A_TakeInventory("FistSwung",1)
        TNT1 A 6
        goto Fired
    FireHit2B:
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE2,0,0)
        IRP4 GHI 1 Radius_Quake(4,10,0,16,0)
        IRP4 J 1 A_Recoil(3)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE2,0,0)
        IRP4 K 1
        IRP4 L 1 A_TakeInventory("FistSwung",1)
        TNT1 A 0 A_TakeInventory("FistHit",1)
        TNT1 A 6
        goto Fired

    Hold:
        TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("HenshinActivated",1,"Unhenshin")
        TNT1 A 0 A_JumpIfInventory("DoubleTapBack",1,"RepelWave")
        TNT1 A 0 A_JumpIfInventory("DoubleTapForward",1,"LungeAttack")
        TNT1 A 0 A_JumpIfInventory("DoubleTapLeft",1,"SpinAttackLeft")
        TNT1 A 0 A_JumpIfInventory("DoubleTapRight",1,"SpinAttackRight")
        TNT1 A 0 A_Refire
        TNT1 A 1
        TNT1 A 0 A_ClearRefire
        goto Fired2
    Unhenshin:
        TNT1 A 1
        TNT1 A 0 A_GiveInventory("HenshinDeactivation",1)
        TNT1 A 35
        goto Ready

    Fired:
        TNT1 A 10 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
    Fired2:
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("FistSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        Goto Select2



    SpinAttackLeft:
        TNT1 A 1 A_SetAngle(angle+11.25)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_PlayWeaponSound("iron/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackLeftContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackLeftContinue:
        IRSP G 1 A_SetAngle(angle+45)
        IRSP H 1 A_SetAngle(angle+50)
        IRSP I 1 A_SetAngle(angle+55)
        IRSP J 1 A_SetAngle(angle+55)
        IRSP K 1 A_SetAngle(angle+50)
        IRSP L 1 A_SetAngle(angle+45)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 1 A_SetAngle(angle+11.25)
        goto SpecialAttackFinish

    SpinAttackRight:
        TNT1 A 1 A_SetAngle(angle-11.25)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_PlayWeaponSound("iron/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackRightContinue")
        TNT1 A 0 ThrustThingZ(0,28,0,0)
    SpinAttackRightContinue:
        IRSP M 1 A_SetAngle(angle-45)
        IRSP N 1 A_SetAngle(angle-50)
        IRSP O 1 A_SetAngle(angle-55)
        IRSP P 1 A_SetAngle(angle-55)
        IRSP Q 1 A_SetAngle(angle-50)
        IRSP R 1 A_SetAngle(angle-45)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 1 A_SetAngle(angle-11.25)
        goto SpecialAttackFinish

    LungeAttack:
        TNT1 A 0 A_CheckFloor("LungeAttackRise")
    LungeAttackPrepare:
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 A 0 A_ChangeFlag("GHOST",1)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE,0,0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 1
        TNT1 A 0 A_PlayWeaponSound("iron/lunge")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 AA 0 A_SpawnItemEx("RazorWindExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,125,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,-1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,-2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,-3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,4,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,-4,48)
        TNT1 A 0 ThrustThing(angle*256/360,120,1,0)
    LungeAttackContinue:
        IRSP ABCDEF 1
        TNT1 A 1
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_Stop // You'd think this would be redundant.
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE,0,0)
        TNT1 A 5 A_SpawnItemEx("TelefragHack",0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        goto SpecialAttackFinish
    LungeAttackRise:
        TNT1 A 1 ThrustThingZ(0,24,0,0)
        goto LungeAttackPrepare

    RepelWave:
        TNT1 A 0
        TNT1 A 0 A_PlayWeaponSound("iron/block")
        IRSP STUVWW 1
        IRSP X 15
        IRSP YZ 1
        IRSP [ 1
        TNT1 A 5
        IRS2 AB 1
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_PlayWeaponSound("iron/beam")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        IRS2 C 1
        TNT1 A 0 A_FireCustomMissile("RazorWind2",0,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-15,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",15,0,0,0,0,0)
        IRS2 DEF 1
        TNT1 AA 1
        goto SpecialAttackFinish

    SpecialAttackFinish:
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_TakeInventory("DoubleTapBack", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapLeft", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapRight", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapForward", 1)
        goto Fired2



    AltFire:
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"AltFireSequence1")
        IRNF BCD 1
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_Jump(128,"AltFireSequence2")
        goto AltFireSequence1

    AltFireSequence1: // Right to left
        TNT1 A 0 A_JumpIfInventory("FistSwung",1,"AltFireSequence2")
        TNT1 A 0
    AltFireSequence1A:
        IRNB AB 1
        TNT1 A 0 A_PlayWeaponSound("holyshot/fire")
        TNT1 A 0 A_FireCustomMissile("HolyShot",0,random(-100,100)/300.00,-8,0,0,random(-100,100)/300.00)
        TNT1 A 0 A_Recoil(1)
        TNT1 A 0 A_SetAngle(angle+(random(-400,400)/1500.000))
        TNT1 A 0 A_SetPitch(pitch-(random(200,700)/1500.000))
        TNT1 A 0 A_GiveInventory("FistSwung",1)
        IRNB D 1 BRIGHT A_ZoomFactor(0.975,ZOOM_INSTANT)
        IRNB E 1 BRIGHT A_ZoomFactor(1.0,ZOOM_INSTANT)
        IRNB F 1 A_WeaponReady(WRF_NOSWITCH)
        goto AltFired
    AltFireSequence2:
        IRNB GH 1
        TNT1 A 0 A_PlayWeaponSound("holyshot/fire")
        TNT1 A 0 A_FireCustomMissile("HolyShot",0,random(-100,100)/300.00,8,0,0,random(-100,100)/300.00)
        TNT1 A 0 A_Recoil(1)
        TNT1 A 0 A_SetAngle(angle+(random(-400,400)/1500.000))
        TNT1 A 0 A_SetPitch(pitch-(random(200,700)/1500.000))
        TNT1 A 0 A_TakeInventory("FistSwung",1)
        IRNB J 1 BRIGHT A_ZoomFactor(0.975,ZOOM_INSTANT)
        IRNB K 1 BRIGHT A_ZoomFactor(1.0,ZOOM_INSTANT)
        IRNB L 1 A_WeaponReady(WRF_NOSWITCH)
        goto AltFired

    AltFired:
        TNT1 A 15 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_Refire
        TNT1 A 20 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("FistSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_ClearRefire
        Goto Select2

    AltHold:
        TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("IronChargeCounter",25,"IronChargeUp")
        TNT1 A 0 A_GiveInventory("IronChargeCounter",1)
        TNT1 A 0 A_Refire
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_TakeInventory("IronChargeCounter",35)
        TNT1 A 0 A_JumpIfInventory("IronCharged",1,"AltFireCharged")
        TNT1 A 30 A_WeaponReady
        goto AltFired
    IronChargeUp:
        TNT1 A 0 A_TakeInventory("IronChargeCounter",35)
        TNT1 A 0 A_JumpIfInventory("IronCharged",1,"AltHold")
        TNT1 A 0 A_GiveInventory("IronCharged",1)
        TNT1 A 0 A_PlayWeaponSound("holyrocket/ready")
        goto AltHold
    AltFireCharged:
        IRNB MN 1
        TNT1 A 0 A_SetBlend("white",0.25,15)
        TNT1 A 0 A_PlayWeaponSound("holyRocket/fire")
        TNT1 A 0 A_FireCustomMissile("HolyRocket",0,random(-50,50)/500.00,6,0,0,random(-50,50)/500.00)
        TNT1 A 0 A_Recoil(5)
        TNT1 A 0 A_SetAngle(angle+(random(-500,500)/1000.000))
        TNT1 A 0 A_SetPitch(pitch-(random(300,700)/1000.000))
        IRNB O 5 BRIGHT A_ZoomFactor(0.95,ZOOM_INSTANT)
        IRNB P 4 BRIGHT A_ZoomFactor(0.975,ZOOM_INSTANT)
        IRNB Q 3 BRIGHT A_ZoomFactor(1.0,ZOOM_INSTANT)
        IRNB R 2 A_TakeInventory("IronCharged",1)
        IRNB S 1 A_ClearRefire
        goto AltFired
    }
}

actor IronFistAttack1 : FastProjectile
{
    Radius 1
    Height 1
    Damage (150)
    Speed 96
    RenderStyle Add
    Alpha 0.6
    Scale 0.65
    SeeSound "none"
    DeathSound "none"
    Species "Player"
    DamageType "IronFistDamage"
    Projectile
    +BLOODSPLATTER
    +THRUSPECIES
    +THRUGHOST
    +EXTREMEDEATH
    +FORCERADIUSDMG
    Decal WallCrack
    Obituary "%k snapped %o with a brutal punch."
    States
    {
    Spawn:
        TNT1 A 1
        stop
    Death:
    Crash:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("FistHit",1)
        TNT1 A 0 A_PlaySound("iron/miss")
        TNT1 AAAAAA 0 A_SpawnItemEx("IronMaidenImpactSmoke",0,0,0,random(-150,150)/50.00,random(-150,150)/50.00,random(-150,150)/50.00,0,160)
        TNT1 AAAAAA 0 A_SpawnItemEx("HammerSmoke",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        FHFX SS 1
        FHFX TU 2
        FHFX VW 3
        stop
    XDeath:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("FistHit",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",10)
        TNT1 A 0 A_PlaySound("iron/hit")
        TNT1 A 0 A_SpawnItemEx("IronFistExplosion",0,0,0,0,0,0,0,48)
        TNT1 AAAAAA 0 A_SpawnItemEx("HammerSmoke",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        FHFX SS 1  
        FHFX TU 2
        FHFX VW 3
        stop
    }
}

ACTOR IronFistExplosion
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    -SOLID
    DamageType "IronFistExplode"
    Obituary "%k snapped %o with a brutal punch."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(96,128,0)
        Stop
    }
}

actor IronFistGuard : KharonGuard {}

actor IronMaidenImpactSmoke
{
    Scale 0.25
    Alpha 0.45
    Renderstyle Add
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        SMOK ABCDEFGHIJKLMNOPQ 1
        Stop
    }
}

ACTOR HolyShotDeath
{
    Renderstyle Add
    Alpha 0.99
    Scale 0.2
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        BSEX AAAAAAAAAAAAAAAAAAAAAAA 1 BRIGHT A_FadeOut(0.075)
        Stop
    }
}

actor HolyShot
{
    Height 8
    Radius 6
    Speed 50
    Damage (65)
    Renderstyle Add
    Alpha 0.9
    Scale 0.5
    DeathSound "holyshot/hit"
    Species "Player"
    DamageType "IronMaidenShot"
    Decal DoomImpScorch
    Projectile
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
    +NOEXTREMEDEATH
    States
    {
    Spawn:
        SPKW ABCDEF 1 BRIGHT A_SpawnItemEx("HolyTrail",0,0,0,0,0,0,0,160)
        Loop
    XDeath:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
    Death:
        TNT1 A 0
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HolyShotPuffs",0,0,0,random(-100,100)/100.0,random(-100,100)/100.0,random(-100,100)/100.0,0,160)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HolyShotPuffs2",0,0,0,random(-100,100)/30.0,random(-100,100)/30.0,random(-100,100)/30.0,0,160)  
        TNT1 A 0 A_SpawnItemEx("HolyShotDeath",0,0,0,0,0,0,0,32)
        TNT1 A 1
        Stop
    }
}

actor HolyRocket : HolyShot
{
    Height 12
    Radius 8
    Speed 40
    Scale 0.8
    Damage (60)
    DeathSound "holyrocket/hit"
    Decal Scorch
    -NOEXTREMEDEATH
    States
    {
    Spawn:
        SPKW ABCDEF 1 BRIGHT A_SpawnItemEx("HolyTrail2",0,0,0,0,0,0,0,160)
        Loop
    XDeath:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
    Death:
        TNT1 A 0
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HolyShotPuffs",0,0,0,random(-100,100)/50.0,random(-100,100)/50.0,random(-100,100)/50.0,0,160)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HolyShotPuffs2",0,0,0,random(-100,100)/15.0,random(-100,100)/15.0,random(-100,100)/15.0,0,160)  
        TNT1 A 0 A_SpawnItemEx("HolyExplosionSpawner",0,0,0,0,0,0,0,160)
        TNT1 A 0 A_SpawnItemEx("HolyRocketJumpage",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("HolyShotExplode",0,0,0,0,0,0,0,48)
        TNT1 A 1
        Stop
    }
}

actor HolyExplosionSpawner
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER)
        TNT1 A 1 BRIGHT 
        Loop
    Toaster0:
        TNT1 A 0 
        TNT1 AAAAA 0 A_SpawnItemEx("HolyRocketExplosion2", 0,0,0, random(200,400)/200.00,0,random(0,200)/200.00, random(0, 360))
        TNT1 AAAAAAAA 0 A_SpawnItemEx("HolyRocketExplosion", 0,0,0, random(300,800)/200.00,0,random(300,800)/200.00, random(0,360))
        TNT1 AAAAAAAA 0 A_SpawnItemEx("HolyRocketExplosion", 0,0,0, random(1500,2500)/200.00,0,random(100,400)/200.00, random(0,360))
        TNT1 A 8
        stop
    Toaster1:
        TNT1 A 0 
        TNT1 AAA 0 A_SpawnItemEx("HolyRocketExplosion2", 0,0,0, random(200,400)/200.00,0,random(0,200)/200.00, random(0, 360))
        TNT1 AAAAA 0 A_SpawnItemEx("HolyRocketExplosion", 0,0,0, random(300,800)/200.00,0,random(300,800)/200.00, random(0,360))
        TNT1 AAAAA 0 A_SpawnItemEx("HolyRocketExplosion", 0,0,0, random(1500,2500)/200.00,0,random(100,400)/200.00, random(0,360))
        TNT1 A 8
        stop
    Toaster2:
        TNT1 A 0 
        TNT1 AA 0 A_SpawnItemEx("HolyRocketExplosion2", 0,0,0, random(200,400)/200.00,0,random(0,200)/200.00, random(0, 360))
        TNT1 AAA 0 A_SpawnItemEx("HolyRocketExplosion", 0,0,0, random(300,800)/200.00,0,random(300,800)/200.00, random(0,360))
        TNT1 AAA 0 A_SpawnItemEx("HolyRocketExplosion", 0,0,0, random(1500,2500)/200.00,0,random(100,400)/200.00, random(0,360))
        TNT1 A 8
        stop
    }
}

actor HolyRocketExpTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Alpha 0.2
    Scale 0.625
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(255,1,2,3,4,5,6,7,8,9)
        EXR3 ABCDEFGHIJKLMNOP 1 bright
        stop
    }
}

actor HolyRocketExplosion
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Alpha 0.5
    Scale 1.125
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(255,1,2,3,4,5,6,7,8,9)
        EXR1 ABCDEFGHIJKLMNOPQRSTUVW 1 bright A_SpawnItemEx("HolyRocketExpTrail", 0,0,0, random(-20, 20)/10.0, random(-20, 20)/10.0, random(-20, 20)/10.0)
        stop
    }
}

actor HolyRocketExplosion2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Alpha 0.55
    Scale 0.75
    RenderStyle Add
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(255,1,2,3,4)
        EXR2 ABCDEFGHIJKL 3 bright A_FadeOut(0.05)
        stop
    }
}

ACTOR HolyShotExplode
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    -SOLID
    +DONTBLAST
    +FORCERADIUSDMG
    DamageType "IronMaidenShot"
    Obituary "%k consumed %o in light."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(196,256,0)
        Stop
    }
}

ACTOR HolyRocketJumpage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    -SOLID
    +DONTBLAST
    DamageType "ExodusJump"
    Obituary "%k shouldn't have killed %o with this."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(160,192,1)
        Stop
    }
}

actor HolyTrail
{
    Renderstyle Add
    Alpha 0.5
    Scale 0.4
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 A 0 A_SpawnItemEx("HolyShotPuffs3",0,0,0,random(-100,100)/40.0,random(-100,100)/40.0,random(-100,100)/40.0,0,32)
        SPKW A 1 BRIGHT A_FadeOut(0.075)
        TNT1 A 0 A_SpawnItemEx("HolyShotPuffs3",0,0,0,random(-100,100)/40.0,random(-100,100)/40.0,random(-100,100)/40.0,0,32)
        SPKW B 1 BRIGHT A_FadeOut(0.075)
        TNT1 A 0 A_SpawnItemEx("HolyShotPuffs3",0,0,0,random(-100,100)/40.0,random(-100,100)/40.0,random(-100,100)/40.0,0,32)
        SPKW C 1 BRIGHT A_FadeOut(0.075)
        TNT1 A 0 A_SpawnItemEx("HolyShotPuffs3",0,0,0,random(-100,100)/40.0,random(-100,100)/40.0,random(-100,100)/40.0,0,32)
        SPKW D 1 BRIGHT A_FadeOut(0.075)
        TNT1 A 0 A_SpawnItemEx("HolyShotPuffs3",0,0,0,random(-100,100)/40.0,random(-100,100)/40.0,random(-100,100)/40.0,0,32)
        SPKW E 1 BRIGHT A_FadeOut(0.075)
        TNT1 A 0 A_SpawnItemEx("HolyShotPuffs3",0,0,0,random(-100,100)/40.0,random(-100,100)/40.0,random(-100,100)/40.0,0,32)
        SPKW F 1 BRIGHT A_FadeOut(0.075)
        Loop
    }
}

actor HolyTrail2 : HolyTrail { Scale 0.7 }

actor HolyShotPuffs
{
    RenderStyle Add
    Scale 0.35
    Alpha 0.5
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 A 0
        LBSS AAAAABBBBB 2 bright A_FadeOut(random(1,3)/100.0)
        loop
    }
}

actor HolyShotPuffs2 : HolyShotPuffs { Scale 0.25 }

actor HolyShotPuffs3 : HolyShotPuffs
{
    Scale 0.15
    Alpha 0.275
    States
    {
    Spawn:
        TNT1 A 0
        LBSS AAAAABBBBB 1 bright A_FadeOut(random(1,3)/100.0)
        loop
    }
}