actor "Kharon + Exodus" : HwandoBase //replaces RocketLauncher
{
    -CHEATNOTWEAPON
    Inventory.PickupMessage "You got the Exodus handheld grenade laucher!"
    Weapon.AmmoUse2 1
    Weapon.AmmoGive2 10
    Weapon.AmmoType2 "WeebRocket"
    Weapon.SlotNumber 5
    States
    {
    Spawn:
        GLAU A -1
        Stop
    Ready:
        WEPE A 1 A_WeaponReady
        Loop
    Select:
        TNT1 A 0 A_JumpIfInventory("AlreadyInLevel",1,"Select3")
        TNT1 AAAAAAA 0 A_Raise
        TNT1 AA 1 A_Raise
        WAC1 C 0 A_PlayWeaponSound("sword/draw")
        TNT1 A 1 A_Raise
        KTUP ABCDE 1 A_Raise
        KTUP FG 1
        TNT1 A 12 A_GiveInventory("AlreadyInLevel",1)
        WEPE DCBA 1
        Goto Ready
    Select2:
        WEPE DCB 1 A_WeaponReady
        goto Ready
    Select3:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop
    IdleAnimation:
        WEPE ABCD 2 A_WeaponReady
        IDLE ABC 2 A_WeaponReady
        IDLE C 15 A_WeaponReady
        IDLE DEFG 2 A_WeaponReady
        IDLE G 15 A_WeaponReady
        IDLE HIJKLMNOPQRSTUV 2 A_WeaponReady
        IDLE V 15 A_WeaponReady
        IDLE WXY 2 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        WEPE DCBA 2 A_WeaponReady
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        goto Ready



    Fire:
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"FirePrepare")
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"FireSequence1")
        WEPE BCD 1
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1
    FirePrepare:
        WEPE IH 1 A_TakeInventory("ShootingLikeABaka")
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1

    FireSequence1: // Right to left
        TNT1 A 0 A_PlaySoundEX("sword/swingmed","SoundSlot7")//A_PlayWeaponSound("wp/KaTaNa1")
        TNT1 A 0 A_JumpIfInventory("KatanaSwung",1,"FireSequence2")
        TNT1 A 0 A_Jump(255,"FireSequence1A","FireSequence1B","FireSequence1C")
        TNT1 A 0
    FireSequence1A:
        KATN F 1
        KATN G 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN H 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN I 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN J 1 A_GiveInventory("KatanaSwung",1)
        TNT1 A 1
        goto Fired
    FireSequence1B:
        KATN K 1
        KATN L 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN M 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN N 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN O 1 A_GiveInventory("KatanaSwung",1)
        KATN P 1
        goto Fired
    FireSequence1C:
        KATN V 1
        KATN W 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN X 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN Y 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN Z 1 A_GiveInventory("KatanaSwung",1)
        TNT1 A 1
        goto Fired

    FireSequence2: // Left to right
        TNT1 A 0 A_Jump(255,"FireSequence2A","FireSequence2B","FireSequence2C")
        TNT1 A 0
    FireSequence2A:
        KATN A 1
        KATN B 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN C 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN D 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN E 1 A_TakeInventory("KatanaSwung",1)
        TNT1 A 1
        goto Fired
    FireSequence2B:
        KTUP A 1
        KTUP B 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KTUP C 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KTUP D 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KTUP E 1 A_TakeInventory("KatanaSwung",1)
        KTUP F 1
        goto Fired
    FireSequence2C:
        KATN Q 1
        KATN R 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN S 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)//A_CustomPunch(30,1,0,"SwordPuff",112)
        KATN T 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN U 1 A_TakeInventory("KatanaSwung",1)
        TNT1 A 1
        goto Fired

    Hold:
        TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("DoubleTapBack",1,"RepelWave")
        TNT1 A 0 A_JumpIfInventory("DoubleTapForward",1,"LungeAttack")
        TNT1 A 0 A_JumpIfInventory("DoubleTapLeft",1,"SpinAttackLeft")
        TNT1 A 0 A_JumpIfInventory("DoubleTapRight",1,"SpinAttackRight")
        TNT1 A 0 A_Refire
        TNT1 A 1
        TNT1 A 0 A_ClearRefire
        goto Fired2

    Fired:
        TNT1 A 10 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
    Fired2:
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        Goto Select2



    SpinAttackLeft:
        TNT1 A 1 A_SetAngle(angle+11.25)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KatanaExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackLeftContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackLeftContinue:
        KSPC G 1 A_SetAngle(angle+45)
        KSPC H 1 A_SetAngle(angle+50)
        KSPC I 1 A_SetAngle(angle+55)
        KSPC J 1 A_SetAngle(angle+55)
        KSPC K 1 A_SetAngle(angle+50)
        KSPC L 1 A_SetAngle(angle+45)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 1 A_SetAngle(angle+11.25)
        goto SpecialAttackFinish

    SpinAttackRight:
        TNT1 A 1 A_SetAngle(angle-11.25)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KatanaExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackRightContinue")
        TNT1 A 0 ThrustThingZ(0,28,0,0)
    SpinAttackRightContinue:
        KSPC M 1 A_SetAngle(angle-45)
        KSPC N 1 A_SetAngle(angle-50)
        KSPC O 1 A_SetAngle(angle-55)
        KSPC P 1 A_SetAngle(angle-55)
        KSPC Q 1 A_SetAngle(angle-50)
        KSPC R 1 A_SetAngle(angle-45)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 1 A_SetAngle(angle-11.25)
        goto SpecialAttackFinish

    LungeAttack:
        TNT1 A 0 A_CheckFloor("LungeAttackRise")
    LungeAttackPrepare:
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 A 0 A_ChangeFlag("GHOST",1)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE,0,0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 1
        TNT1 A 0 A_PlayWeaponSound("sword/beam3")
        TNT1 AA 0 A_SpawnItemEx("RazorWindExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,125,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,-1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,-2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,-3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,4,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,-4,48)
        TNT1 A 0 ThrustThing(angle*256/360,120,1,0)
    LungeAttackContinue:
        KSPC ABCDEF 1
        TNT1 A 1
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_Stop // You'd think this would be redundant.
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE,0,0)
        TNT1 A 5 A_SpawnItemEx("TelefragHack",0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        goto SpecialAttackFinish
    LungeAttackRise:
        TNT1 A 1 ThrustThingZ(0,24,0,0)
        goto LungeAttackPrepare

    RepelWave:
        KBLK ABCDE 1
        KATN F 1
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/reflect")
        TNT1 A 0 A_FireCustomMissile("RazorWind2",0,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-2,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",2,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-4,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",4,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-8,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",8,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-10,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",10,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-14,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",14,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-16,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",16,0,0,0,0,0)
        KATN GHIJ 1
        TNT1 AA 1
        goto SpecialAttackFinish

    SpecialAttackFinish:
        TNT1 A 0 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_TakeInventory("CantStopTheBlock",1)
        TNT1 A 0 A_TakeInventory("DoubleTapBack", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapLeft", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapRight", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapForward", 1)
        goto Fired2



    AltFire:
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"AltFirePrepare")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"AltFireGun")
        WEPE EFG 1
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
    AltFireGun:
        GLAG A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfNoAmmo("Nope")
        TNT1 A 0 A_Light1
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlayWeaponSound("carronade/fire")
        TNT1 A 0 A_FireCustomMissile("ExodusRocket",random(-350,350)/100.00,1,5,1,0,random(10,250)/100.00)
        TNT1 A 0 A_Recoil(2)
        TNT1 A 0 Radius_Quake(2,3,0,16,0)
        TNT1 A 0 A_SetAngle(angle+(random(-2000,2000)/1000.000))
        TNT1 A 0 A_SetPitch(pitch-(random(800,1800)/1000.000))
        GLAG BB 1 Bright A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        GLAG BB 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_PlaySoundEx("carronade/turn","soundslot5",0,0)
        TNT1 A 0 A_Light0
        GLAG CCCDDDEEAA 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        GLAG AA 1 A_WeaponReady(WRF_NOSWITCH)
        GLAG A 70 A_WeaponReady
        Goto AltFired
    Nope:
        TNT1 A 0 A_PlaySoundEx("carronade/dry","soundslot7",0,0)
        GLAG A 10 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        GLAG A 35 A_Weaponready
        Goto AltFired
    AltFirePrepare:
        WEPE H 1 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
        WEPE I 1
        goto AltFireGun
    AltFired:
        TNT1 A 0 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        WEPE GFE 1
        Goto Ready
    }
}

ACTOR ExodusRocket
{
    Radius 6
    Height 4
    Damage (10)
    Speed 50
    Species "EnemyShot"
    Projectile
    Decal Scorch
    +THRUSPECIES
    +THRUGHOST
    +RANDOMIZE
    +GRENADETRAIL
    -NOGRAVITY
    Gravity 0.25
    Scale 0.4
    DeathSound "grenades/explode"
    States
    {
    Spawn:
        HORN A 1 Bright A_CustomMissile("ExodusRocketFlameSpawner",2,0,0,0)
        TNT1 A 0 A_CustomMissile("ExodusRocketFlameSpawner2",2,0,random(-100,100)/150.00,2,random(-100,100)/150.00)
        Loop
    Death:
        TNT1 A 0 A_Scream
        TNT1 A 0 A_SpawnItemEx("ExodusRocketDamage",96,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ExodusRocketDamage2",96,0,0,0,0,0,90,48)
        TNT1 A 0 A_SpawnItemEx("ExodusRocketDamage",96,0,0,0,0,0,180,48)
        TNT1 A 0 A_SpawnItemEx("ExodusRocketDamage2",96,0,0,0,0,0,270,48)
        TNT1 A 0 A_SpawnItemEx("ExodusRocketJumpage",0,0,0,0,0,0,0,48)
        TNT1 A 0 Radius_Quake(5,3,0,8,0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAA 0 A_SpawnItemEx("MissileExplosionSmall",0,0,0,0,0,0,0,160)
        TNT1 AAA 0 A_SpawnItemEx("MissileExplosionLarge",0,0,0,0,0,0,0,160)
        TNT1 AAA 0 A_SpawnItemEx("MissileExplosionSmall",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("MissileExplosionLarge",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-180,180),160)
        TNT1 A 1
        Stop
    }
}

ACTOR ExodusRocketDamage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    -SOLID
    DamageType "ExodusDamage"
    Obituary "%k consumed %o in fire."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(64,192,0)
        Stop
    }
}

actor ExodusRocketDamage2 : ExodusRocketDamage { +FORCERADIUSDMG }

ACTOR ExodusRocketJumpage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    -SOLID
    DamageType "ExodusJump"
    Obituary "%k shouldn't have killed %o with this."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(160,192,1)
        Stop
    }
}

ACTOR ExodusRocketFlameSpawner
{
    Projectile
    Speed 0
    Scale 0.15
    Alpha 0.5
    Height 0
    Renderstyle Add
    +NOCLIP
    +NOTELEPORT
    +NOBLOCKMAP
    -FLOORCLIP
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 AA 1
        XPLO AB 2 BRIGHT 
        XPLO C 2 BRIGHT A_CustomMissile("ExodusRocketFlameTrail",1,random(-100,100)/100.00,2,random(-100,100)/100.00)
        Stop
    }
}

ACTOR ExodusRocketFlameSpawner2 : ExodusRocketFlameSpawner { Scale 0.1 }

ACTOR ExodusRocketFlameTrail
{
    PROJECTILE
    Height 1
    Radius 1
    Scale 0.16
    Alpha 0.3
    Renderstyle Translucent
    States
    {
    Spawn:
        SMOK ABCDEFGHIJKLMNOP 1
        SMOK Q 1 A_FadeOut
        Stop
    }
}

actor MissileExplosionSmall
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.75
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4","Spawn5","Spawn6","Spawn7")
        TNT1 A 1
        stop
    Spawn1:
        MXP1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn2:
        MXP2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn3:
        MXP3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn4:
        MXP4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn5:
        MXP5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn6:
        MXP6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn7:
        MXP7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    }
}

actor MissileExplosionLarge : MissileExplosionSmall
{
    Alpha 0.65
    Scale 1.25
}

Actor FireTrail
{
    Radius 4
    Height 2
    Scale 0.4
    Gravity 0.2
    BounceCount 2
    RenderStyle Add
    Alpha 0.99
    Projectile
    -NOGRAVITY
    +EXPLODEONWATER
    +NOTELEPORT
    +DOOMBOUNCE
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        1090 Q 1 BRIGHT A_FadeOut(0.2)
        1090 R 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        1090 S 1 BRIGHT
        1090 T 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        1090 U 1 BRIGHT
        1090 V 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        1090 W 1 BRIGHT
        1090 X 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        loop
    death:
        1090 QRSTUVWX 1
        stop
    }
}

Actor FireTrail2 : FireTrail
{
    States
    {
    Spawn:
        1091 ABCDE 3 BRIGHT
        stop
    }
}