// Weapon/item spawners for Skulltag-specific maps. Wahey!

actor InvisibilitySphere : SentinelPickup 5035//replaces InvisibilitySphere
{
    Game Doom
    SpawnID 174
}

actor RedArmor : BlindGuardianPickup 5040//replaces RedArmor
{
    Game Doom
    SpawnID 168
}

actor MaxArmorBonus : SoulStoneSpawner1 5091//replaces MaxArmorBonus
{
    Game Doom
    SpawnID 167
}

actor MaxHealthBonus : SoulStoneSpawner1 5090//replaces MaxHealthBonus
{
    Game Doom
    SpawnID 166
}

// Monster spawners
actor Abaddon : Wizard 5015
{
    Game Doom
    Health 1200
    SpawnID 220
    Radius 22
    Height 56
    Speed 12
    Mass 400
    Species "Body"
    PainChance "KharonSheathe", 255
    PainChance "kubiuti", 255
    PainChance "SabbathDamage", 192
    PainChance "IronFistDamage", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "KubiRangeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    SeeSound "wizard/see"
    PainSound "wizard/pain"
    DeathSound "wizard/death"
    ActiveSound "wizard/idle"
    //BloodColor blue
    DropItem ""
    PainChance 128
    Monster
    +FLOAT
    +NOGRAVITY
    +DONTOVERLAP
    +MISSILEMORE
    Obituary "%o was struck down by a Disciple."
    States
    {
    Spawn:
        DISC AB 10 A_Look
        Loop
    See:
        DISC A 3 A_Chase
        DISC A 4 A_FastChase
        DISC A 3 A_Chase
        DISC A 4 A_FastChase
        DISC B 3 A_Chase
        DISC B 4 A_FastChase
        DISC B 3 A_Chase
        DISC B 4 A_FastChase
        Loop
    Missile:
        TNT1 A 0 A_PlaySound("wizard/shotprepare")
        DISC C 4 A_WizAtk1
        DISC C 4 A_WizAtk2
        DISC C 4 A_WizAtk1
        DISC C 4 A_WizAtk2
        DISC C 4 A_WizAtk1
        DISC C 4 A_WizAtk2
        DISC C 4 A_WizAtk1
        DISC C 4 A_WizAtk2
        TNT1 A 0 A_GhostOff
        TNT1 A 0 A_PlaySound("wizard/shotfire")
        TNT1 A 0 A_PlaySoundEx("wizard/shotpew","soundslot6",0,0)
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,0)
        DISC D 1 Bright // One tic delay so if the player's in his face, he only gets one point of damage instead of three.
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,6)
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,-6)
        DISC D 12 Bright
        Goto See
    Pain:
        DISC E 3 A_GhostOff
        DISC E 3 A_Pain
        Goto See

    Death.Kubiuti:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("enemy/bisected")
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",2)
    Death:
        DISC F 6 A_GhostOff
        DISC G 6 A_Scream
        DISC HI 6
        TNT1 AAA 0 A_SpawnItemEx("WizardPuff",0,0,40,random(-50,50)/100.00,random(-50,50)/100.00,random(25,100)/100.00,random(-180,180),160)
        DISC J 6 A_NoBlocking
        DISC KL 6
        DISC M -1 A_SetFloorClip
        Stop

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain
    Pain.KubiRangeAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain
    Death.KubiRangeAttack:
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
    Death.Knife:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        //TNT1 A 0 A_GiveToTarget("SuperMeterCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_PlaySound("enemy/bisected")
        goto death//.kubiuti+2
    Pain.SabbathDamage:
    Pain.KharonSheathe:
        DISC E 5 A_GhostOff
        DISC E 5 A_Pain
        Goto See
    Death.IronFistDamage:
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",2)
        goto Death
    Death.UltimaBeam:
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_Scream
        DISC EEEEEEEE 2 A_FadeOut(0.1)
        TNT1 A 0 A_NoBlocking
        NONE A 1
        stop
    }
}

actor Belphegor : WeeabooBaronOfHell 5008
{
    Game Doom
    SpawnID 215
    Health 1500
    Scale 1.25
    Speed 15
    PainChance 25
    Obituary "%o was slain by a Belphegor."
    HitObituary "%o was slain by a Belphegor."
    Translation "16:47=176:191", "48:79=80:111"
}

actor BloodDemon : WeeabooDemon 5004
{
    Game Doom
    SpawnID 156
    Speed 15
    Health 300
    Obituary "%o was chomped on by a Blood Demon."
    Translation "16:35=28:47", "36:47=[75,0,0]:[0,0,0]", "160:162=168:168", "163:166=164:167", "224:230=165:165", "161:161=164:164", "80:111=[200,200,200]:[0,0,0]", "208:223=177:188"
}

actor Cacolantern : Abaddon 5006
{
    Game Doom
    SpawnID 159
    Health 800
    Translation "176:183=200:207", "184:191=240:247"
    DropItem ""
    States
    {
    See:
        DISC A 3 A_Chase
        DISC A 4 A_Chase
        DISC A 3 A_Chase
        DISC A 4 A_Chase
        DISC B 3 A_Chase
        DISC B 4 A_Chase
        DISC B 3 A_Chase
        DISC B 4 A_Chase
        Loop
    }
}

actor DarkImp : WeeabooImp 5003
{
    Game Doom
    SpawnID 155
    Health 120
    Obituary "%o was burned by a Dark Imp."
    HitObituary "%o was slashed by a Dark Imp."
    BloodColor Blue
    Translation "1:1=8:8", "16:47=[0,0,255]:[0,0,0]", "48:79=[185,185,185]:[0,0,0]", "151:151=6:6", "160:167=198:204", "169:174=194:200", "175:183=197:205", "184:185=206:207", "186:191=240:245", "208:223=193:207", "232:235=201:204"//"48:79=[185,185,185]:[0,0,0]", "175:183=197:205", "169:174=194:200", "184:185=206:207", "186:191=240:245", "16:47=[0,0,255]:[0,0,0]", "232:235=201:204", "160:167=198:204", "208:223=193:207"
    States
    {
    Melee:
    Missile:
        TROO E 1 A_FaceTarget
        TNT1 A 0 A_PlaySound("imp/preparefire")
        TNT1 A 0 A_SpawnItemEx("WeeabooImpBallPrepare",2,28,32,0,0,0,0,160)
        TROO EEEF 6 A_FaceTarget
        TNT1 A 0 A_PlayWeaponSound("imp/shootfire")
        TROO G 5 A_CustomMissile("WeeabooImpBall")//A_TroopAttack
        TNT1 A 0 A_Jump(192,"See")
        TNT1 A 0 A_SpidRefire
        Goto See
    }
}

actor Hectebus : WeeabooFatty 5007
{
    Game Doom
    SpawnID 158
    Health 1200
    PainChance 20
    Obituary "%o was cremated by a Hectebus."
    BloodColor Green
    Translation "48:63=80:95", "64:79=96:111", "1:2=5:6", "13:15=106:111", "236:239=108:111", "144:151=103:111", "16:47=112:127", "208:223=80:111", "176:191=112:127", "168:175=112:127", "160:167=112:127", "232:236=96:108"
    States
    {
    Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        FATT G 15 A_FatRaise
        TNT1 A 0 A_PlaySound("fatso/attack")
        FATT H 0 A_CustomMissile("HectShot",28,0,0,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,16.875,1)
        FATT H 10 Bright
        FATT IG 5 A_FaceTarget
        TNT1 A 0 A_PlaySound("fatso/attack")
        FATT H 0 A_CustomMissile("HectShot",28,0,0,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,-16.875,1)
        FATT H 10 Bright
        FATT IG 5 A_FaceTarget
        TNT1 A 0 A_PlaySound("fatso/attack")
        FATT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,0,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
        FATT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
        FATT H 10 Bright
        FATT IG 5
        TNT1 A 0 A_Jump(192,"See")
        TNT1 A 0 A_SpidRefire
        Goto Missile
    }
}

actor HectShot : BaronBall//FatShot - Surprisingly, the Baron Ball looks better.
{
    DamageType "HeavyEnemyDamage"
    +THRUGHOST
    Scale 1.5
    Translation "176:191=112:127", "208:223=112:123", "168:175=112:115", "231:231=112:112", "32:47=121:121", "160:167=112:127", "232:235=112:127", "61:79=115:124", "128:143=112:127", "16:31=112:115", "144:151=121:127", "13:15=122:122"
}

actor SuperShotgunGuy : WeeabooChaingunner 5005
{
    Game Doom
    SpawnID 157
    Health 120
    Obituary "%o was pounded by a white zombie."
    Translation "64:79=48:79", "30:47=112:127"
    States
    {
    See:
        CPOS AABBCCDD 4 A_FastChase
        Loop
    Missile:
        TNT1 A 0 A_PlaySound("chngnner/gunready")
        CPOS E 30 A_FaceTarget
        TNT1 A 0 A_FaceTarget
        CPOS F 0 A_PlaySound("chngnner/fire")
        CPOS F 4 bright A_CustomMissile("EnemyBullet",30,15,random(-100,100)/100.00)
        TNT1 A 0 A_FastChase
        CPOS E 4 A_CPosRefire
        Goto Missile+2
    }
}