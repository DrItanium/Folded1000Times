ACTOR SentinelHealth : Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 200
}

actor AllFearTheSentinel
{
    Health 200
    Radius 16
    Height 12
    Mass 0x7FFFFFFF
    Speed 0
    PainChance 256
    MinMissileChance 10
    Scale 0.3
    Monster
    -SOLID
    +NOCLIP
    +FLOAT
    +FRIENDLY
    +NOGRAVITY
    +MISSILEMORE
    +MISSILEEVENMORE
    +DONTFALL
    +NOBLOOD
    +LOOKALLAROUND
    +NOTARGET
    +QUICKTORETALIATE
    +THRUSPECIES
    Species "Player"
    SeeSound "sentinel/see"
    PainSound "sentinel/pain"
    DeathSound "sentinel/dead"
    ActiveSound "sentinel/idle"
    Obituary "%o was condemned to Hell by the Sentinel."
    DamageFactor "kubiuti", 0.0
    DamageFactor "kubispinattack", 0.0
    DamageFactor "kubilungeattack", 0.0
    DamageFactor "kubirangeattack", 0.0
    DamageFactor "HaltHammerzeitImpact", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "IronFistDamage", 0.0
    DamageFactor "IronFistExplosion", 0.0
    DamageFactor "AcaciaDamage", 0.0
    DamageFactor "SabbathDamage", 0.0
    DamageFactor "ExodusDamage", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "Knife", 0.0
    DamageFactor "TestamentDamage", 0.0
    DamageFactor "ChemiKill", 0.0
    RadiusDamageFactor 0.0
    States
    {
    Spawn:
        TNT1 A 0 A_GiveInventory("SentinelIdle",1)
        SENT A 5 A_Look
        Loop
    ClearTarget:
        TNT1 A 0 A_ClearTarget
        TNT1 A 0 A_TakeInventory("SentinelMidCombat",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SentinelIdleCounter",0x7FFFFFFF)
        SENT A 1 A_PlaySoundEx("sentinel/unload","soundslot6",0,0)
        SENT A 1 A_PlaySoundEx("sentinel/nope","soundslot7",0,0)
        goto Spawn
    See:
        SENT A 2 A_Chase
        TNT1 A 0 A_GiveInventory("SentinelIdleCounter",1)
        TNT1 A 0 A_JumpIfInventory("SentinelIdleCounter",70,"ClearTarget")
        Loop
    Missile:
        TNT1 A 0 A_JumpIfInventory("SentinelMidCombat",1,"Missile2")
        SENT A 12 A_PlaySound("sentinel/load")
        TNT1 A 0 A_SpidRefire
    Missile2:
        TNT1 A 0 A_TakeInventory("SentinelIdle",1)
        TNT1 A 0 A_TakeInventory("SentinelIdleCounter",0x7FFFFFFF)
        TNT1 A 0 A_GiveInventory("SentinelMidCombat",1)
        TNT1 A 0 A_PlayWeaponSound("sentinel/fire")
        TNT1 AA 0 A_CustomMissile("SentinelBullet",0,0,random(-300,300)/100.00)
        SENT B 2 Bright
        SENT A 3
        TNT1 A 0 A_CPosRefire
        Goto Missile2
    Pain:
        SENT A 2
        SENT A 5 A_Pain
        goto See
    Death:
        SENT A 1
        SENT A 1 A_Scream
        SENT AAAAAAAAAA 1 A_FadeOut(0.1)
        TNT1 A 0 A_GiveInventory("SentinelBeyondTheRealmsOfDeath",1)
        TNT1 A 10
        stop
    }
}