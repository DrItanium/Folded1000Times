// THE SENTINEL
//   Hell has Imps. Heaven has Putti. Tiny, angelic souls that sing the praises
// of the Lord daily, in modern media they have been depicted as tiny boys and
// girls with cute faces and blushing, rosy cheeks.
//   Not quite.
//   The crux of all Heavenly armies, combat-ready Putti (nicknamed Sentinels)
// act as basic security, scouts, and skirmishers, cannon fodder to support the
// higher-ranking angels as best as they can. Wielding dual heavy machine guns
// and an eternally-regenerating source of ammo integrated into their body, they
// have the firepower and an itchy trigger finger, but unfortunately lack both
// the durability and strategic intuition to be much more than a nuisance before
// getting swatted aside by a higher-ranking demon.

//   That's where you come in.
//   Every now and then, perhaps through sheer luck, there remains a Sentinel
// somehow still alive on the field of battle. Upon recognizing an ally needing
// support, they will hang near and follow close, acting as a mobile turret and
// promptly firing away at any enemy within sight and range. Naturally obedient
// and eternally eager to serve, the Sentinel will follow any ally to its death,
// supporting to the best of its ability.
//   Though by itself it may be little more than mere cannon-fodder...with your
// protection and guidance, even a lowly Putti can become a valuable ally
// capable of striking a new fear into the armies of Hell.

ACTOR SentinelHealth : Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 200
}

actor AllFearTheSentinel
{
    Health 200
    Radius 16
    Height 12
    Mass 0x7FFFFFFF
    Speed 0
    PainChance 256
    MinMissileChance 10
    Scale 0.3
    Monster
    -SOLID
    +NOCLIP
    +FLOAT
    +FRIENDLY
    +NOGRAVITY
    +MISSILEMORE
    +MISSILEEVENMORE
    +DONTFALL
    +NOBLOOD
    +LOOKALLAROUND
    +NOTARGET
    +QUICKTORETALIATE
    +THRUSPECIES
    Species "Player"
    SeeSound "sentinel/see"
    PainSound "sentinel/pain"
    DeathSound "sentinel/dead"
    ActiveSound "sentinel/idle"
    Obituary "%o was condemned to Hell by the Sentinel."
    DamageFactor "kubiuti", 0.0
    DamageFactor "kubispinattack", 0.0
    DamageFactor "kubilungeattack", 0.0
    DamageFactor "kubirangeattack", 0.0
    DamageFactor "KharonHookshot", 0.0
    DamageFactor "HaltHammerzeitImpact", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "IronFistDamage", 0.0
    DamageFactor "IronFistExplosion", 0.0
    DamageFactor "AcaciaDamage", 0.0
    DamageFactor "SabbathDamage", 0.0
    DamageFactor "ExodusDamage", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "Knife", 0.0
    DamageFactor "TestamentDamage", 0.0
    DamageFactor "Frosthammer", 0.0
    DamageFactor "FrosthammerCharged", 0.0
    DamageFactor "FrosthammerExplode", 0.0 // Isn't used, but just in case.
    DamageFactor "ChemiKill", 0.0
    DamageFactor "Telefrag", 0.0
    DamageFactor "Taser", 0.0
    DamageFactor "EMPulse", 0.0
    DamageFactor "SentinelDamage", 0.0
    DamageFactor "ThatsSoRaven", 0.0
    DamageFactor "Stomp", 0.0
    DamageFactor "Mastodon", 0.0
    DamageFactor "Triumph", 0.0
    DamageFactor "TriumphExplode", 0.0
    RadiusDamageFactor 0.0
    States
    {
    Spawn:
        TNT1 A 0 A_GiveInventory("SentinelIdle",1)
        SENT A 5 A_Look
        Loop
    ClearTarget:
        TNT1 A 0 A_ClearTarget
        TNT1 A 0 A_TakeInventory("SentinelShotCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SentinelMidCombat",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SentinelIdleCounter",0x7FFFFFFF)
        SENT A 1 A_PlaySoundEx("sentinel/unload","soundslot6",0,0)
        SENT A 1 A_PlaySoundEx("sentinel/nope","soundslot7",0,0)
        goto Spawn
    See:
        TNT1 A 0 A_JumpIfInTargetInventory("ImTheBlindGuardianYouDumbass",1,"ClearTarget")
        SENT A 2 A_Chase
        TNT1 A 0 A_GiveInventory("SentinelIdleCounter",1)
        TNT1 A 0 A_JumpIfInventory("SentinelIdleCounter",70,"ClearTarget")
        Loop
    Missile:
        TNT1 A 0 A_JumpIfInTargetInventory("ImTheBlindGuardianYouDumbass",1,"ClearTarget")
        TNT1 A 0 A_JumpIfInventory("SentinelMidCombat",1,"Missile2")
        SENT A 12 A_PlaySound("sentinel/load")
        TNT1 A 0 A_SpidRefire
    Missile2:
        TNT1 A 0 A_JumpIfInventory("SentinelShotCounter",0,"Pause")
        TNT1 A 0 A_GiveInventory("SentinelShotCounter",random(1,3))
        TNT1 A 0 A_JumpIfInTargetInventory("ImTheBlindGuardianYouDumbass",1,"ClearTarget")
        TNT1 A 0 A_TakeInventory("SentinelIdle",1)
        TNT1 A 0 A_TakeInventory("SentinelIdleCounter",0x7FFFFFFF)
        TNT1 A 0 A_GiveInventory("SentinelMidCombat",1)
        TNT1 A 0 A_PlayWeaponSound("sentinel/fire")
        TNT1 AA 0 A_CustomMissile("SentinelBullet",0,0,random(-200,200)/100.00)
        SENT B 2 Bright
        SENT A 3
        TNT1 A 0 A_CPosRefire
        Goto Missile2
    Pause:
        TNT1 A 0 A_TakeInventory("SentinelShotCounter",0x7FFFFFFF)
        SENT A 6
        SENT A 0 A_PlaySoundEx("sentinel/cock","soundslot5",0,0)
        SENT A 16
        goto Missile2
    Pain:
        SENT A 2
        SENT A 5 A_Pain
        goto See
    Death:
        SENT A 1
        SENT A 1 A_Scream
        SENT AAAAAAAAAA 1 A_FadeOut(0.1)
        TNT1 A 0 A_GiveInventory("SentinelBeyondTheRealmsOfDeath",1)
        TNT1 A 10
        stop
    }
}