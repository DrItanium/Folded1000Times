actor 999Health: Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 999
}

ACTOR MeleePlayer : PlayerPawn
{
    Health 100
    Radius 12
    Height 48
    Mass 100
    PainChance 255
    Speed 1
    +NOSKIN
    -NORADIUSDMG
    -THRUSPECIES
    Species "Player"
    Gravity 0.85
    Player.JumpZ 9.0
    Player.ForwardMove 2.4, 1.2
    Player.SideMove 2.3, 1.15
    Player.DisplayName "Hae-Lin"
    PainChance "EnemyInfightDamage", 0
    DamageFactor "Normal", 0.75
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "GuardMove", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "Telefrag", 2.00
    DamageFactor "Suicide", 2.00
    RadiusDamageFactor 0.5
    Player.StartItem "Kharon + Acacia A-22", 1
    Player.StartItem "KamenRiderModeActivate", 20
    Player.StartItem "SuperMeterCounter", 20
    Player.StartItem "WeebClip", 20
    Player.ColorRange 112, 127
    Player.AttackZOffset 16
    States
    {
    Spawn:
        WARR A -1
        loop
    See:
        WARR ABCD 4
        Loop
    Missile:
    Melee:
        WARR EF 8 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        Goto Spawn
    Pain.Drowning:
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",3,"Pain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",2,"PainWater2")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",1,"PainWater1")
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("B2E0F0",0.25,35)
        goto See
    PainWater1:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("B2E0F0",0.5,35)
        goto See
    PainWater2:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("B2E0F0",0.75,35)
        goto See
    Pain.Lava:
    Pain.Fire:
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",3,"Pain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",2,"PainLava2")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",1,"PainLava1")
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("FF9900",0.5,35)
        goto See
    PainLava1:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("FF9900",0.75,35)
        goto See
    PainLava2:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("FF9900",0.9,35)
        goto See
    Pain.Slime:
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",3,"Pain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",2,"PainSlime2")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",1,"PainSlime1")
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("6B886B",0.25,35)
        goto See
    PainSlime1:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("6B886B",0.5,35)
        goto See
    PainSlime2:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("6B886B",0.75,35)
        goto See
    Pain:
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_TakeInventory("EnviroDamageCount",3)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_DAMAGE,0,0)
        TNT1 A 0 A_JumpIfInventory("ContraArmorToken",1,"PainArmored")
        TNT1 A 0 A_SetBlend("Red",1.0,15)
        WARR G 4 A_Recoil(15)
        WARR G 4 A_Pain
        Goto Spawn
    PainArmored:
        TNT1 A 0 A_SetBlend("Yellow",0.5,15)
        TNT1 A 0 A_PlaySound("armor/hit")
        WARR G 4 A_Recoil(5)
        WARR G 4
        Goto Spawn
    Death:
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("ContraLifeToken",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("ContraArmorToken",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("OverLifeToken",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("HyperComboCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",0x7FFFFFFF)
        TNT1 A 0 A_SetBlend("Black",1.0,35)
        WARR H 6
        WARR I 6 A_PlayerScream
        WARR JK 6
        WARR L 6 A_NoBlocking
        WARR M 6
        WARR N -1
        Stop
    /*XDeath:
        TNT1 A 0
        TNT1 A 0 A_SetBlend("Black",1.0,35)
        WARR O 5 A_PlayerScream
        WARR P 5 //A_SkullPop("BloodySkull")
        WARR R 5 A_NoBlocking
        WARR STUV 5
        WARR W -1
        Stop*/
    }
}

actor GhostPort : PowerGhost { Powerup.Duration 10 }

actor KamenRiderModeActivate : CustomInventory
{
    +INVENTORY.UNDROPPABLE
    +INVBAR
    Inventory.Amount 1
    Inventory.MaxAmount 1
    States
    {
    Use:
        TNT1 A 0 A_JumpifInventory("HenshinCooldown",1,"UseFail")
        TNT1 A 0 A_JumpifInventory("HenshinActivated",1,"UseFail") // Technically not needed, but I'd rather not spam the server with ACS.
        TNT1 A 0 A_GiveInventory("HenshinActivated",1)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_HENSHIN,0,0)
        fail
    UseFail:
        TNT1 A 0
        fail
    }
}

actor NewLevelStatReset : CustomInventory
{
    -INVBAR
    +AUTOACTIVATE
    States
    {
    Pickup:
    Use:
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        stop
    }
}

ACTOR SoulAmmo : CustomInventory
{
    Radius 10
    Height 12
    Speed 7
    Damage 0
    Renderstyle Add
    Alpha 0.7
    Scale 0.45
    +QUIET
    +NOGRAVITY
    +SEEKERMISSILE
    +DROPOFF
    +NOBLOCKMONST
    +CANPASS
    -COUNTITEM
    +ALWAYSPICKUP
    +FLOATBOB
    +NOCLIP
    +FORCEXYBILLBOARD
    States
    {
    Spawn:
		TNT1 A 1
    Spawn2:
        ISHA A 1 Bright A_BishopMissileWeave
        TNT1 A 0 A_SeekerMissile(20,35)
        TNT1 A 0 A_SpawnItemEx("SoulTrail",0,0,0,0,0,0,0,160)
        TNT1 A 0
        loop
    PickUp:
        TNT1 A 0
        TNT1 A 0 A_SetBlend("white",0.04,5)
        TNT1 A 0 A_PlaySound("ammo/soul")
        TNT1 A 0 A_GiveInventory("SuperMeterCounter",1)
    Death:
        TNT1 A 1
        //CYBR A 35 
        stop
    }
}

Actor SoulTrail
{
    Scale 0.225
    Alpha 0.7
    Renderstyle Add
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    States
    {
    Spawn:
        TNT1 A 1
    Spawn2:
        ISHT A 1 BRIGHT A_FadeOut(0.1)
        loop
    }
}