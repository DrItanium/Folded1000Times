actor 999Health: Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 999
}

ACTOR MeleePlayer : PlayerPawn
{
    Health 100
    Radius 12
    Height 48
    Mass 100
    PainChance 255
    Speed 1
    //+NOSKIN
    -NORADIUSDMG
    -THRUSPECIES
    +DONTBLAST
    Species "Player"
    XScale 0.4
    YScale 0.38
    Gravity 0.85
    Player.JumpZ 9.0
    Player.ForwardMove 2.4, 1.2
    Player.SideMove 2.3, 1.15
    Player.DisplayName "Hae-Lin"//"Manual Fire"
    Player.SoundClass "Hae-Lin"
    PainChance "EnemyInfightDamage", 0
    DamageFactor "Normal", 0.75
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "FrosthammerCharged", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "GuardMove", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "Falling", 0.0
    DamageFactor "Telefrag", 2.00
    DamageFactor "Suicide", 2.00
    RadiusDamageFactor 0.5
    Player.StartItem "KharonAcacia", 1
    Player.StartItem "Kharon", 1
    //Player.StartItem "Messiah", 1
    Player.StartItem "KamenRiderModeActivate", 1
    Player.StartItem "SuperMeterCounter", 20
    Player.StartItem "WeebClip", 20
    Player.StartItem "UppercutTrigger", 1
    Player.StartItem "UppercutUntrigger", 1
    Player.StartItem "ZDoomTauntButton", 1
    Player.StartItem "IsJungHaeLin", 1
    Player.ColorRange 112, 127
    Player.AttackZOffset 16
    States
    {
    Spawn:
        JHLX A 1 A_JumpIfInventory("GhostStepCooldown",26,"Dodge")
        loop
    See:
        JHLX HHHHIIIIJJJJKKKK 1 A_JumpIfInventory("GhostStepCooldown",26,"Dodge")
        Loop
    Dodge:
        JHLX C 12
    Dodge2:
        JHLX C 1 A_CheckFloor("Spawn")
        loop
    Missile:
    Melee:
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        JHLX FG 8 A_JumpIfInventory("ShootingLikeABaka",1,"GunAttack")
        Goto Spawn
    GunAttack:
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        JHLX DE 8
        Goto Spawn
    Pain.Drowning:
        TNT1 A 0 A_JumpIfInventory("IAmATraditionalDoomerWhoLikesNumbersOverTokens",1,"DoomPain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",3,"Pain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",2,"PainWater2")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",1,"PainWater1")
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("B2E0F0",0.25,35)
        goto See
    PainWater1:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("B2E0F0",0.5,35)
        goto See
    PainWater2:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("B2E0F0",0.75,35)
        goto See
    Pain.Lava:
    Pain.Fire:
        TNT1 A 0 A_JumpIfInventory("IAmATraditionalDoomerWhoLikesNumbersOverTokens",1,"DoomPain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",3,"Pain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",2,"PainLava2")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",1,"PainLava1")
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("FF9900",0.5,35)
        goto See
    PainLava1:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("FF9900",0.75,35)
        goto See
    PainLava2:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("FF9900",0.9,35)
        goto See
    Pain.Slime:
        TNT1 A 0 A_JumpIfInventory("IAmATraditionalDoomerWhoLikesNumbersOverTokens",1,"DoomPain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",3,"Pain")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",2,"PainSlime2")
        TNT1 A 0 A_JumpIfInventory("EnviroDamageCount",1,"PainSlime1")
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("6B886B",0.25,35)
        goto See
    PainSlime1:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("6B886B",0.5,35)
        goto See
    PainSlime2:
        TNT1 A 0 A_GiveInventory("EnviroDamageCount",1)
        TNT1 A 0 A_GiveInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_SetBlend("6B886B",0.75,35)
        goto See
    Pain:
        TNT1 A 0 A_JumpIfInventory("IAmATraditionalDoomerWhoLikesNumbersOverTokens",1,"DoomPain")
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("EnviroDamageCooldown",75)
        TNT1 A 0 A_TakeInventory("EnviroDamageCount",3)
        TNT1 A 0 ACS_ExecuteWithResult(WEEB_DECORATE,WEEB_DEC_DAMAGE,0,0)//ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_DAMAGE,0,0)
        TNT1 A 0 A_JumpIfInventory("ContraArmorToken",1,"PainArmored")
        TNT1 A 0 A_SetBlend("Red",1.0,15)
        JHLX B 4 A_Recoil(15)
        JHLX B 4 A_Pain
        Goto Spawn
    PainArmored:
        TNT1 A 0 A_SetBlend("Yellow",0.5,15)
        TNT1 A 0 A_PlaySound("armor/hit")
        JHLX B 4 A_Recoil(5)
        JHLX B 4
        Goto Spawn
    DoomPain:
        JHLX B 4
        JHLX B 4 A_Pain
        goto Spawn
    Death:
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("ContraLifeToken",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("ContraArmorToken",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("OverLifeToken",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("HyperComboCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",0x7FFFFFFF)
        TNT1 A 0 A_SetBlend("Black",1.0,35)
        JHLX L 6
        JHLX M 6 A_PlayerScream
        JHLX NO 6
        JHLX P 6 A_NoBlocking
        JHLX P -1
        Stop
    }
}

actor MeleePlayerCasual : MeleePlayer
{
    Player.DisplayName "Automatic Fire"
    Player.StartItem "KharonAcacia", 1
    Player.StartItem "Kharon", 1
    Player.StartItem "KamenRiderModeActivate", 1
    Player.StartItem "SuperMeterCounter", 20
    Player.StartItem "WeebClip", 20
    Player.StartItem "UppercutTrigger",1
    Player.StartItem "UppercutUntrigger",1
    Player.StartItem "ZDoomTauntButton", 1
    Player.StartItem "IAmAMassiveCasualWhoNeedsMyWeaponsToFireAutomatically", 1
}

actor GhostPort : PowerGhost { Powerup.Duration 10 }

actor KamenRiderModeActivate : CustomInventory
{
    +INVENTORY.UNDROPPABLE
    +INVBAR
    Inventory.Amount 1
    Inventory.MaxAmount 1
    Tag " "
    States
    {
    Use:
        TNT1 A 0 A_JumpIfInventory("GotIronMaiden",1,"UseCheck")
        TNT1 A 1
        fail
    UseCheck:
        TNT1 A 0 A_JumpifInventory("HenshinCooldown",1,"UseFail")
        TNT1 A 0 A_JumpifInventory("HenshinActivated",1,"UseFail") // Technically not needed, but I'd rather not spam the server with ACS.
        TNT1 A 0 A_GiveInventory("HenshinActivated",1)
        TNT1 A 0 ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_HENSHIN,0,0)
        TNT1 A 1
        fail
    UseFail:
        TNT1 A 0
        fail
    }
}

actor DoubleJumpWingTrigger : CustomInventory // If Z& had SpawnForced for ACS, I wouldn't need to do this.
{
    Inventory.MaxAmount 0
    Inventory.Amount 0
    +INVENTORY.AUTOACTIVATE
    States
    {
    Pickup:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("DoubleJumpWings",-32,0,32,0,0,0,0,32)
        stop
    }
}

actor DoubleJumpWings
{
    Radius 1
    Height 1
    Scale 0.3
    RenderStyle Add
    Alpha 0.99
    -SOLID
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        ANWN AAAAAAAAAA 1 BRIGHT A_FadeOut(0.1)
        stop
    }
}

actor IronMaidenHalo
{
    Radius 1
    Height 1
    Scale 0.13
    RenderStyle Add
    Alpha 0.99
    -SOLID
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        IMHL O -1 BRIGHT
        stop
    }
}

actor NewLevelStatReset : CustomInventory
{
    -INVBAR
    +AUTOACTIVATE
    States
    {
    Pickup:
    Use:
        TNT1 A 0 ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_UNFREEZE2,0,0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        stop
    }
}

ACTOR SoulAmmo : CustomInventory
{
    Radius 10
    Height 12
    Speed 7
    Damage 0
    Renderstyle Add
    Alpha 0.6
    Scale 0.45
    +QUIET
    +NOGRAVITY
    +SEEKERMISSILE
    +DROPOFF
    +NOBLOCKMONST
    +CANPASS
    -COUNTITEM
    +ALWAYSPICKUP
    +FLOATBOB
    +NOCLIP
    +FORCEXYBILLBOARD
    States
    {
    Spawn:
        TNT1 A 1
    Spawn2:
        ISHA A 1 Bright A_BishopMissileWeave
        TNT1 A 0 A_SeekerMissile(20,35)
        TNT1 A 0 A_SpawnItemEx("SoulTrail",0,0,0,0,0,0,0,160)
        TNT1 A 0 A_FadeOut(0.002)
        loop
    PickUp:
        TNT1 A 0
        TNT1 A 0 A_SetBlend("white",0.04,5)
        TNT1 A 0 A_PlaySound("ammo/soul")
        TNT1 A 0 A_GiveInventory("SuperMeterCounter",1)
        TNT1 A 0 A_GiveInventory("Points",1)
    Death:
        TNT1 A 1
        //CYBR A 35 
        stop
    }
}

Actor SoulTrail
{
    Scale 0.25
    Alpha 0.4
    Renderstyle Add
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    States
    {
    Spawn:
        TNT1 A 1
    Spawn2:
        ISHT A 1 BRIGHT A_FadeOut(0.075)
        loop
    }
}

actor HaeLinFoodSmallComment : CustomInventory
{
    Inventory.MaxAmount 0
    +AUTOACTIVATE
    States
    {
    Spawn:
        TNT1 A 0
        stop
    Pickup:
        TNT1 A 0 A_Jump(32,"PickupComment")
        stop
    PickupComment:
        TNT1 A 0  ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_PLAYSOUND,2,0)//A_PlaySoundEx("haelin/foodsmall", 7)
        stop
    }
}

actor HaeLinFoodLargeComment : CustomInventory
{
    Inventory.MaxAmount 0
    +AUTOACTIVATE
    States
    {
    Spawn:
        TNT1 A 0
        stop
    Pickup:
        TNT1 A 0 A_Jump(64,"PickupComment")
        stop
    PickupComment:
        TNT1 A 0  ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_PLAYSOUND,3,0)//A_PlaySoundEx("haelin/foodlarge", 7)
        stop
    }
}

actor HaeLinFoodRamenComment : CustomInventory
{
    Inventory.MaxAmount 0
    +AUTOACTIVATE
    States
    {
    Spawn:
        TNT1 A 0
        stop
    Pickup:
        TNT1 A 0 A_Jump(96,"PickupComment")
        stop
    PickupComment:
        TNT1 A 0  ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_PLAYSOUND,4,0)//A_PlaySoundEx("haelin/foodramen", 7)
        stop
    }
}

actor ZDoomTauntButton : CustomInventory
{
    -INVBAR
    +UNDROPPABLE
    Inventory.Amount 1
    Inventory.MaxAmount 1
    states
    {
    Use:
        TNT1 A 0 A_JumpIfInventory("NoTauntAllowed",1,"UseFail")
        TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
        fail
    UseFail:
        TNT1 A 0
        fail
    }
}

ACTOR UppercutTrigger : CustomInventory
{
    -INVBAR
    +UNDROPPABLE
    Inventory.Amount 1
    Inventory.MaxAmount 1
    States
    {
    Use:
        TNT1 A 0 A_GiveInventory("TimeToLaunchOctopus", 1)
        Fail
    }
}

ACTOR UppercutUntrigger : CustomInventory
{
    -INVBAR
    +UNDROPPABLE
    Inventory.Amount 1
    Inventory.MaxAmount 1
    States
    {
    Use:
        TNT1 A 0 A_TakeInventory("TimeToLaunchOctopus", 1)
        Fail
    }
}