ACTOR HwandoBase : Weapon
{
    Weapon.KickBack 50
    Weapon.SlotNumber 1
    Weapon.AmmoUse 0
    Weapon.AmmoUse2 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    Weapon.AmmoType2 ""
    Obituary "%k tore %o apart."
    DamageType "kubiuti"
    Decal BulletChip
    +Weapon.AXEBLOOD
    +NOALERT
    +NOAUTOFIRE
    +NOEXTREMEDEATH
    +ALT_AMMO_OPTIONAL
    +CHEATNOTWEAPON
    States
    {
    Spawn:
        KATP A -1
        stop
    Ready:
        SWRD A 1 A_WeaponReady
        Loop
    Deselect:
        TNT1 AA 0 A_Lower
        SWRD A 1 A_Lower
        Loop
    Select:
        TNT1 A 0 A_JumpIfInventory("AlreadyInLevel",1,"Select2")
        TNT1 AAAAAAA 0 A_Raise
        TNT1 AA 1 A_Raise
        WAC1 C 0 A_PlayWeaponSound("sword/draw")
        TNT1 A 1 A_Raise
        KTUP ABCDE 1 A_Raise
        KTUP FG 1
        TNT1 A 12 A_GiveInventory("AlreadyInLevel",1)
        SWRC GHIJ 1
        Goto Ready
    Select2: 
        TNT1 A 0 A_Raise
        SWRD AA 1 A_Raise
        Loop
    Fire:
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"Hold")
        SWRC ACE 1
        TNT1 A 0 A_JumpIfInventory("DoubleTapBack",1,"Block")
        TNT1 A 0 A_JumpIfInventory("DoubleTapForward",1,"LungeAttack")
        TNT1 A 0 A_JumpIfInventory("DoubleTapLeft",1,"SpinAttackLeft")
        TNT1 A 0 A_JumpIfInventory("DoubleTapRight",1,"SpinAttackRight")
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
    Hold:
    FireSequence1: // Right to left
        TNT1 A 0 A_PlaySoundEX("sword/swingmed","SoundSlot7")//A_PlayWeaponSound("wp/KaTaNa1")
        TNT1 A 0 A_JumpIfInventory("KatanaSwung",1,"FireSequence2")
        TNT1 A 0 A_Jump(255,"FireSequence1A","FireSequence1B","FireSequence1C")
        TNT1 A 0
    FireSequence1A:
        KATN F 1
        KATN G 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN H 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN I 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN J 1 A_GiveInventory("KatanaSwung",1)
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 1
        goto Fired
    FireSequence1B:
        KATN K 1
        KATN L 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN M 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN N 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN O 1 A_GiveInventory("KatanaSwung",1)
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN P 1
        goto Fired
    FireSequence1C:
        KATN V 1
        KATN W 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN X 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN Y 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN Z 1 A_GiveInventory("KatanaSwung",1)
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 1
        goto Fired

    FireSequence2: // Left to right
        TNT1 A 0 A_Jump(255,"FireSequence2A","FireSequence2B","FireSequence2C")
        TNT1 A 0
    FireSequence2A:
        KATN A 1
        KATN B 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN C 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN D 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN E 1 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 1
        goto Fired
    FireSequence2B:
        KTUP A 1
        KTUP B 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KTUP C 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KTUP D 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KTUP E 1 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KTUP F 1
        goto Fired
    FireSequence2C:
        KATN Q 1
        KATN R 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN S 1
        TNT1 A 0 A_CustomPunch(30,1,0,"SwordPuff",96)
        KATN T 1
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        KATN U 1 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_FireCustomMissile("KatanaGuard",0,0) 
        TNT1 A 1
        goto Fired

    Fired:
        TNT1 A 10 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH/* | WRF_NOSECONDARY*/)
        TNT1 A 0 A_ReFire
        TNT1 A 1 A_TakeInventory("KatanaSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        TNT1 AAAAAAA 0 A_Lower
        Goto Select2

    AltFire:
        SWRC ACE 1
    AltFireGun:
        TNT1 A 4
        goto Fired

    SpinAttackLeft:
        TNT1 A 1 A_SetAngle(angle+11.25)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KatanaExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackLeftContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackLeftContinue:
        KATN K 1 A_SetAngle(angle+45)
        KATN L 1 A_SetAngle(angle+50)
        KATN M 1 A_SetAngle(angle+55)
        KATN N 1 A_SetAngle(angle+55)
        KATN O 1 A_SetAngle(angle+50)
        KATN P 1 A_SetAngle(angle+45)
        TNT1 A 1 A_SetAngle(angle+22.5)
		TNT1 A 1 A_SetAngle(angle+11.25)
        goto SpecialAttackFinish

    SpinAttackRight:
        TNT1 A 1 A_SetAngle(angle-11.25)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KatanaExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackRightContinue")
        TNT1 A 0 ThrustThingZ(0,28,0,0)
    SpinAttackRightContinue:
        KTUP A 1 A_SetAngle(angle-45)
        KTUP B 1 A_SetAngle(angle-50)
        KTUP C 1 A_SetAngle(angle-55)
        KTUP D 1 A_SetAngle(angle-55)
        KTUP E 1 A_SetAngle(angle-50)
        KTUP F 1 A_SetAngle(angle-45)
        TNT1 A 1 A_SetAngle(angle-22.5)
		TNT1 A 1 A_SetAngle(angle-11.25)
        goto SpecialAttackFinish

    SpecialAttackFinish:
		TNT1 A 0 A_TakeInventory("DoubleTapBack", 1)
		TNT1 A 0 A_TakeInventory("DoubleTapLeft", 1)
		TNT1 A 0 A_TakeInventory("DoubleTapRight", 1)
		TNT1 A 0 A_TakeInventory("DoubleTapForward", 1)
        goto Fired

    LungeAttack:
        TNT1 A 0 A_CheckFloor("LungeAttackRise")
    LungeAttackPrepare:
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 A 0 A_ChangeFlag("GHOST",1)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE,0,0)
        TNT1 A 0 A_PlayWeaponSound("sword/beam3")
        TNT1 AA 0 A_SpawnItemEx("RazorWindExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,125,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,-1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,-2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,-3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,4,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,-4,48)
        TNT1 A 0 ThrustThing(angle*256/360,120,1,0)
    LungeAttackContinue:
        KATN FGHIJ 1 //A_CustomPunch(30,1,0,"SwordPuff",96) - Nope, jerks the camera.
        TNT1 A 2
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_Stop // You'd think this would be redundant.
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE,0,0)
        TNT1 A 5 A_SpawnItemEx("TelefragHack",0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        goto SpecialAttackFinish
    LungeAttackRise:
        TNT1 A 1 ThrustThingZ(0,24,0,0)
        goto LungeAttackPrepare

    Block:
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_Stop
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE,0,0)
        KBLK ABCCDD 1
        TNT1 A 0 A_PlayWeaponSound("sword/block")
    BlockProcess:
        TNT1 A 0 A_JumpIfInventory("FakeAttack",1,"BlockStop")
        TNT1 A 0 A_JumpIfInventory("StopTheBlock",1,"BlockReflect")
        TNT1 A 0 A_JumpIfInventory("BlockLife",105,"BlockStop")
        KBLK E 1 A_FireCustomMissile("KatanaGuard2",0,0)
        TNT1 A 0 A_GiveInventory("BlockLife",1)
        loop
    BlockStop:
        KBLK DCB 1
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE,0,0)
        KBLK A 1 A_TakeInventory("BlockLife",0x7FFFFFFF)
        TNT1 A 1
        goto SpecialAttackFinish
    BlockReflect:
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",10)
        TNT1 A 0 A_PlayWeaponSound("sword/reflect")
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE,0,0)
        TNT1 A 0 A_TakeInventory("StopTheBlock",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("BlockLife",0x7FFFFFFF)
        KATN VWXYZ 1 A_TakeInventory("StopTheBlock",0x7FFFFFFF) // For some reason it's not always getting taken, so here.
        TNT1 AA 1 A_TakeInventory("StopTheBlock",0x7FFFFFFF) // BRUTE FORCE CODING, FUCKER
        goto SpecialAttackFinish
    }
}