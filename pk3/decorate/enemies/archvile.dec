actor WeeabooArchvile : Archvile replaces Archvile
{
    Species "Body"
    PainChance "kubiuti", 64
    PainChance "SabbathDamage", 128
    PainChance "IronFistDamage", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "KubiRangeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    +MISSILEMORE
    +MISSILEEVENMORE
    States
    {
    Missile:
        TNT1 A 0 A_Jump(16,"MissileOriginal")
    Missile2:
        VILE GH 9 bright A_FaceTarget
        VILE IJKLM 9 bright A_SpawnItemEx("SnappyArchieFire",12,0,0,random(0,2),0,random(0,1),random(-4,4),/*SXF_TRANSFERPOINTERS|*/SXF_NOCHECKPOSITION|SXF_SETMASTER)
        VILE NOPP 9 bright
        TNT1 A 0 A_Jump(16,"MissileOriginal")
        TNT1 A 0 A_Jump(32,"Missile2")
        goto see
    MissileOriginal:
        VILE G 0 Bright A_VileStart
        VILE G 10 Bright A_FaceTarget
        VILE H 8 Bright A_VileTarget
        VILE IJKLMN 8 Bright A_FaceTarget
        VILE O 8 Bright A_VileAttack
        VILE P 20 Bright
        Goto See

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Super::Pain
    Pain.KubiRangeAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Super::Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
    Death.KubiRangeAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        //TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto death.kubiuti+6
    Pain.SabbathDamage:
        VILE Q 6 //A_Recoil(2)
        VILE Q 6 A_Pain
        Goto See

    Death.Rocket:
        TNT1 A 0
        XDJ1 BBBBB 1 A_SpawnDebris("BloodShower2")
        TNT1 AAAAAAAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        XDJ1 CCCCC 1 A_SpawnDebris("BloodShower2")
        XDJ1 DEFGHI 5 A_SpawnDebris("BloodShower2")
        XDJ1 J -1
        Stop
    Death.RocketB:
        TNT1 A 0
        XDJ1 KKKKK 1 A_SpawnDebris("BloodShower2")
        TNT1 AAAAAAAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        XDJ1 LLLLL 1 A_SpawnDebris("BloodShower2")
        XDJ1 MNOPQR 5 A_SpawnDebris("BloodShower2")
        XDJ1 S -1
        Stop
    Death.Kubiuti:
        TNT1 A 0 A_AlertMonsters
        TNT1 AAAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",5)
        TNT1 A 0 A_KillChildren
        TNT1 A 0 A_Jump(128,"Death.Rocket","Death.RocketB")    
        XDJ2 B 5 A_PlaySound("Enemy/Bisected")
        XDJ2 BBBB 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        XDJ2 B 0 A_Scream
        XDJ2 C 0 A_NoBlocking
        XDJ2 BBBBB 1 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDJ2 CCCCC 1 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDJ2 DEFGH 5 A_SpawnDebris("BloodShower2")
        XDJ2 I -1
        Stop

    Death.IronFistDamage:
        TNT1 AAAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",3)
        TNT1 A 0
        TNT1 AAAAAAAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(0,32),0,random(0,360),2,random(0,360))
        VILE Q 2
        TNT1 A 0 A_XScream
        TNT1 AAAAAAAAAAAA 0 A_CustomMissile("BloodShower2",random(16,48),0,180+random(-45,45),2,random(0,75))
        TNT1 AAAAAA 0 A_CustomMissile("EnemySplashGus1",random(0,32),0,random(0,360),2,random(0,360))
        TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("TinyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("TinyBouncyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib4",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib6",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib7",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib8",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(32,48),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        AXD1 AA 1 A_SpawnDebris("BloodShower2",1)
        TNT1 A 0 A_NoBlocking
        AXD1 BBBCCCDDEEFFGG 1 A_SpawnDebris("BloodShower2",1)
        AXD1 H -1
        stop
    }
}

// Nicked from Vaecrius' Make It Snappy. Great work, dude!

actor SnappyArchieFire
{
    Radius 16
    Height 32
    Mass 14
    Health 20
    Speed 4
    VSpeed 0.1
    RenderStyle Add
    Alpha 0.9
    Species "Body"
    SeeSound "vile/firecrkl"
    DeathSound "vile/firestrt"
    BloodColor "Gold"
    Monster
    +THRUSPECIES
    -SOLID
    //+NOBLOOD
    +NOGRAVITY
    +FLOATBOB
    +FLOAT
    +LOOKALLAROUND
    +MISSILEMORE
    +MISSILEEVENMORE
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    Obituary "%o was burned by something vile."
    states
    {
    Spawn:
        FIRE CCDDEEDDCCDDEEDDCCDDEEDDCCDDEEDDCCDDEEDDCCDDEEDD 1 bright A_Wander
    Spawn2:
        FIRE CCDDEEDD 1 bright A_Look
        loop
    See:
        FIRE CCDDEEDD 1 bright A_Chase("","Missile",CHF_DONTMOVE)//A_VileChase
        loop
    Missile:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0)
        FIRE CCDDEEDD 1
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0)
        FIRE CCDDEEDD 1
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0)
        FIRE CCDDEEDD 1
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0)
        FIRE CCDDEEDD 1
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0)
        FIRE CCDDEEDD 1
        TNT1 A 0 A_Jump(240,"missile2")
        TNT1 A 0 A_Die
        goto Death
    Missile2:
        FIRE CCDDEEFFGGHHGGFFEEDD 1 bright A_Stop//A_Wander
        goto see
    Death:
        FIRE CDEFGH 1 bright
        TNT1 A 1
        stop
    }
}
actor SnappyArchieFireFire : BaronBall
{
    Translation "112:127=160:167"
    Radius 8
    Height 10
    DamageType "ArchvileFire"
    +THRUGHOST
}