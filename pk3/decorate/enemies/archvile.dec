actor WeeabooArchvile : Archvile replaces Archvile
{
    Species "Body"
    PainChance "kubiuti", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    +MISSILEMORE
    +MISSILEEVENMORE
    States
    {
    Missile:
        TNT1 A 0 A_Jump(16,"MissileOriginal")
    Missile2:
        VILE GH 9 bright A_FaceTarget
        VILE IJKLM 9 bright A_SpawnItemEx("SnappyArchieFire",12,0,0,random(0,2),0,random(0,1),random(-4,4),/*SXF_TRANSFERPOINTERS|*/SXF_NOCHECKPOSITION|SXF_SETMASTER)
        VILE NOPP 9 bright
        TNT1 A 0 A_Jump(16,"MissileOriginal")
        TNT1 A 0 A_Jump(32,"Missile2")
        goto see
    MissileOriginal:
        VILE G 0 Bright A_VileStart
        VILE G 10 Bright A_FaceTarget
        VILE H 8 Bright A_VileTarget
        VILE IJKLMN 8 Bright A_FaceTarget
        VILE O 8 Bright A_VileAttack
        VILE P 20 Bright
        Goto See

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        goto Super::Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto death.kubiuti+2

    death.rocket:
        XDJ1 B 0 A_NoBlocking
        XDJ1 BBBBB 1 A_SpawnDebris("BloodShower2")
        XDJ1 BBBBBBBBBB 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        XDJ1 C 0 A_XScream
        XDJ1 CCCCC 1 A_SpawnDebris("BloodShower2")
        XDJ1 DEFGHI 5 A_SpawnDebris("BloodShower2")
        XDJ1 J -1
        Stop
    death.kubiuti:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",5)
        TNT1 A 0 A_KillChildren
        TNT1 A 0 A_Jump(128,"Death.Rocket")    
        XDJ2 B 5 A_PlaySound("Enemy/Bisected")
        XDJ2 BBBB 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        XDJ2 B 0 A_Scream
        XDJ2 BBBBB 1 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDJ2 C 0 A_NoBlocking
        XDJ2 CCCCC 1 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDJ2 DEFGH 5 A_SpawnDebris("BloodShower2")
        XDJ2 I -1
        Stop
    }
}

// Nicked from Vaecrius' Make It Snappy. Great work, dude!

actor SnappyArchieFire
{
    Radius 16
    Height 32
    Mass 14
    Health 20
    Speed 10
    RenderStyle Add
    Alpha 0.9
    Species "EnemyShot"
    SeeSound "vile/firecrkl"
    DeathSound "vile/firestrt"
    Monster
    +THRUSPECIES
    -SOLID
    +NOBLOOD
    +NOGRAVITY
    +FLOATBOB
    +FLOAT
    +LOOKALLAROUND
    +MISSILEMORE
    +MISSILEEVENMORE
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    Obituary "%o was burned by something vile."
    states
    {
    Spawn:
        FIRE CCDDEEDDCCDDEEDDCCDDEEDDCCDDEEDDCCDDEEDDCCDDEEDD 1 bright A_Wander
    Spawn2:
        FIRE CCDDEEDD 1 bright A_Look
        loop
    See:
        FIRE CCDDEEDD 1 bright A_Chase//A_VileChase
        loop
    Missile:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0)
        TNT1 A 0 A_Jump(240,"missile2")
        TNT1 A 0 A_Die
        goto Death
    Missile2:
        FIRE CCDDEEFFGGHHGGFFEEDD 1 bright A_Wander
        goto see
    Death:
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,45,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,90,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,135,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,180,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,225,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,270,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,315,CMF_AIMDIRECTION)
        FIRE CDEFGH 1 bright
        TNT1 A 1
        stop
    }
}
actor SnappyArchieFireFire : BaronBall
{
    Translation "112:127=160:167"
    Radius 8
    Height 10
    Health 5
    Species "EnemyShot"
    DamageType "ArchvileFire"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "EnemyInfightDamageCyber", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "ExodusDamage", 0.0
    DamageFactor "Kubiuti", 0.0
    +THRUSPECIES
    +THRUGHOST
    +SHOOTABLE
    -NOBLOCKMAP 
    +NOBLOOD
    States
    {
    Death:
        TNT1 A 0 A_UnsetShootable
        goto Super::Death
    }
}