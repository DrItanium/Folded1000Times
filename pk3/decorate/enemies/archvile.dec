actor WeeabooArchvile : Archvile replaces Archvile
{
    Species "Body"
    PainChance "kubiuti", 255
    PainChance "KubiSpinAttack", 255
    PainChance "HaltHammerzeit", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    States
    {
    Missile:
        VILE GH 10 bright A_FaceTarget
        VILE IJKL 10 bright A_SpawnItemEx("SnappyArchieFire",12,0,0,random(0,2),0,random(0,1),random(-4,4),/*SXF_TRANSFERPOINTERS|*/SXF_NOCHECKPOSITION|SXF_SETMASTER)
        VILE MNOPP 10 bright
        goto see

    Pain.KubiSpinAttack:
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        goto Super::Pain
    Death.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
        goto death.kubiuti+1

    death.rocket:
        XDJ1 B 0 A_NoBlocking
        XDJ1 BBBBB 1 A_SpawnDebris("BloodShower2")
        XDJ1 BBBBBBBBBB 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        XDJ1 C 0 A_XScream
        XDJ1 CCCCC 1 A_SpawnDebris("BloodShower2")
        XDJ1 DEFGHI 5 A_SpawnDebris("BloodShower2")
        XDJ1 J -1
        Stop
    death.kubiuti:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",5)
        TNT1 A 0 A_KillChildren
        TNT1 A 0 A_Jump(128,"Death.Rocket")    
        XDJ2 B 5 A_PlaySound("Enemy/Bisected")
        XDJ2 BBBB 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        XDJ2 B 0 A_Scream
        XDJ2 BBBBB 1 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDJ2 C 0 A_NoBlocking
        XDJ2 CCCCC 1 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDJ2 DEFGH 5 A_SpawnDebris("BloodShower2")
        XDJ2 I -1
        Stop
    }
}

// Nicked from Vaecrius' Make It Snappy. Great work, dude!

actor SnappyArchieFire
{
    Radius 16
    Height 32
    Mass 14
    Health 20
    Speed 6
    RenderStyle Add
    Alpha 0.9
    Species "EnemyShot"
    SeeSound "vile/firecrkl"
    DeathSound "vile/firestrt"
    Monster
    +THRUSPECIES
    -SOLID
    +NOBLOOD
    +NOGRAVITY
    +FLOATBOB
    +FLOAT
    +LOOKALLAROUND
    +MISSILEMORE
    +MISSILEEVENMORE
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    Obituary "%o was burned by something vile."
    states
    {
    Spawn:
        FIRE CDEDCDEDCDEDCDEDCDEDCDED 2 bright A_Wander
    Spawn2:
        FIRE CDED 2 bright A_Look
        loop
    See:
        FIRE CDED 2 bright A_Chase//A_VileChase
        loop
    Missile:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0)
        TNT1 A 0 A_Jump(212,"missile2")
        TNT1 A 0 A_Die
    Missile2:
        FIRE CDEFGHGFED 1 bright
        goto see
    Death:
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,0,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,45,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,90,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,135,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,180,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,225,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,270,CMF_AIMDIRECTION)
        TNT1 A 0 A_CustomMissile("SnappyArchieFireFire",32,0,315,CMF_AIMDIRECTION)
        FIRE CDEFGH 1 bright
        TNT1 A 1
        stop
    }
}
actor SnappyArchieFireFire : BaronBall
{
    Translation "112:127=160:167"
    Radius 8
    Height 10
    Health 5
    Species "EnemyShot"
    DamageType "ArchvileFire"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "RocketJumpDamage", 0.0
    +THRUSPECIES
    +THRUGHOST
    +SHOOTABLE
    -NOBLOCKMAP 
    +NOBLOOD
}