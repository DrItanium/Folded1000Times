ACTOR WeeabooWizard replaces Cacodemon //Cacodemon : Cacodemon replaces Cacodemon
{
    Health 450
    Radius 22
    Height 56
    Speed 10
    Mass 400
    Species "Body"
    PainChance "kubiuti", 255
    PainChance "IronFistDamage", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    SeeSound "wizard/see"
    PainSound "wizard/pain"
    DeathSound "wizard/death"
    ActiveSound "wizard/idle"
    //BloodColor blue
    PainChance 128
    Monster
    +FLOAT
    +NOGRAVITY
    +DONTOVERLAP
    +MISSILEMORE
    Obituary "%o was cursed by a dark wizard."
    States
    {
    Spawn:
        WZRD AB 10 A_Look
        loop
    See:
        TNT1 A 0 A_JumpIfCloser(384,"SeeClose")
        WZRD AAB 3 A_FastChase
        WZRD BAA 3 A_Chase
        WZRD BBA 3 A_FastChase
        WZRD ABB 3 A_Chase
        Loop
    SeeClose:
        WZRD AAABBB 2 A_Chase
        goto See
    Melee:
    Missile:
        TNT1 A 0 A_PlaySound("wizard/shotprepare")
        WZRD XYZ 7 Bright A_FaceTarget
        TNT1 A 0 A_FaceTarget
        WZRD DE 5 Bright
        TNT1 A 0 A_PlaySound("wizard/shotfire")
        TNT1 A 0 A_PlaySoundEx("wizard/shotpew","soundslot6",0,0)
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,0)
        WZRD F 1 Bright // One tic delay so if the player's in his face, he only gets one point of damage instead of three.
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,6)
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,-6)
        WZRD F 4 Bright
        WZRD AAABBB 2 A_Wander
        Goto See

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        goto Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        //TNT1 A 0 A_GiveToTarget("SuperMeterCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto death//.kubiuti+2
    Pain:
        WZRD C 3
        WZRD C 3 A_Pain
        Goto See

    Death.Kubiuti:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
    Death:
        WZRD G 6 A_Gravity
        WZRD H 6 Bright A_Scream
        WZRD I 5 Bright A_NoBlocking
        WZRD J 5 Bright
        WZRD K 5 Bright
        WZRD LM 4 Bright
        TNT1 AA 0 A_SpawnItemEx("WizardPuff",0,0,40,random(-50,50)/100.00,random(-50,50)/100.00,random(25,100)/100.00,random(-180,180),160)
        WZRD N 4 Bright A_SpawnItemEx("WizardPuff",0,0,40,0,0,0.5,0,160)
        WZRD O 4 //A_QueueCorpse
        WZRD P -1
        Stop
    Death.IronFistDamage:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
        goto Death
    }
}

ACTOR WizardPuff
{
    +NOBLOCKMAP
    +NOGRAVITY
    RenderStyle Translucent
    Alpha 0.95
    States
    {
    Spawn:
        WZRD QRST 6 Bright A_FadeOut(0.1)
        WZRD UV 7 Bright A_FadeOut(0.1)
        WZRD W 6 Bright A_FadeOut(0.1)
        Stop
    }
}

actor WizardSpell
{
    Radius 6
    Height 8
    Health 5
    Speed 10
    FastSpeed 20
    Damage 5
    RenderStyle Add
    Alpha 1
    Scale 0.75
    Species "EnemyShot"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "EnemyInfightDamageCyber", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "ExodusDamage", 0.0
    DamageFactor "Kubiuti", 0.0
    DamageFactor "IronFistExplode", 0.0
    DamageFactor "HaltHammerzeitImpact", 0.0
    DeathSound "wizard/shothit"
    Projectile
    +THRUSPECIES
    +SHOOTABLE
    +THRUGHOST
    -NOBLOCKMAP 
    +NOBLOOD
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("WizardTrail2",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        SPZR A 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        TNT1 A 0 A_SpawnItemEx("WizardTrail2",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        SPZR B 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        TNT1 A 0 A_SpawnItemEx("WizardTrail2",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        SPZR C 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        TNT1 A 0 A_SpawnItemEx("WizardTrail2",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        SPZR D 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        loop
    Death:
        TNT1 A 0 A_UnsetShootable
        TNT1 A 0 A_SpawnItemEx("EnemyCacoInfight",0,0,0,0,0,0,0,32)
        TNT1 AAAAAA 0 A_SpawnItemEx("WizardTrail1",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,0,160)
        SPZR E 1 BRIGHT A_Scream
        SPZR FGHIJKLMNOP 1 BRIGHT
        stop
    }
}

actor WizardTrail1
{
    Alpha 0.95
    Scale 0.55
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        SPZR Q 1 BRIGHT A_FadeOut(0.1)
        SPZR Q 1 BRIGHT A_FadeOut(0.1)
        SPZR R 1 BRIGHT A_FadeOut(0.1)
        SPZR R 1 BRIGHT A_FadeOut(0.1)
        loop
    }
}

actor WizardTrail2 : WizardTrail1 { Alpha 0.85 Scale 0.25 }