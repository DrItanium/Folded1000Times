ACTOR WeeabooWizard replaces Cacodemon //Cacodemon : Cacodemon replaces Cacodemon
{
    Health 450
    Radius 22
    Height 56
    Speed 10
    Mass 400
    Species "Body"
    PainChance "Triumph", 255
    PainChance "ShihongPunch", 255
    PainChance "ShihongPunchSpecial", 255
    PainChance "KharonSheathe", 255
    PainChance "kubiuti", 255
    PainChance "kubiutiCleave", 255
    PainChance "KharonHookshot", 255
    PainChance "MessiahDamage", 255
    PainChance "KubiutiUpper", 255
    PainChance "IronFistUpper", 255
    PainChance "LittleMacUppercut", 255
    PainChance "ShihongSpinAttack", 255
    PainChance "SabbathDamage", 192
    PainChance "IronFistDamage", 255
    PainChance "IronFistOverkill", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "KubiRangeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    SeeSound "wizard/see"
    PainSound "wizard/pain"
    DeathSound "wizard/death"
    ActiveSound "wizard/idle"
    //BloodColor blue
    PainChance 128
    Monster
    +FLOAT
    +NOGRAVITY
    +DONTOVERLAP
    +MISSILEMORE
    +DONTBLAST
    Obituary "%o was cursed by a dark wizard."
    States
    {
    Spawn:
        WZRD AB 10 A_Look
        loop
    See:
        TNT1 A 0 A_JumpIfCloser(384,"SeeClose")
        WZRD AAB 3 A_FastChase
        WZRD BAA 3 A_Chase
        WZRD BBA 3 A_FastChase
        WZRD ABB 3 A_Chase
        Loop
    SeeClose:
        WZRD AAABBB 2 A_Chase
        goto See
    Melee:
    Missile:
        TNT1 A 0 A_PlaySound("wizard/shotprepare")
        WZRD XYZ 7 Bright A_FaceTarget
        TNT1 A 0 A_FaceTarget
        WZRD DE 5 Bright
        TNT1 A 0 A_PlaySound("wizard/shotfire")
        TNT1 A 0 A_PlaySoundEx("wizard/shotpew","soundslot6",0,0)
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,0)
        WZRD F 1 Bright // One tic delay so if the player's in his face, he only gets one point of damage instead of three.
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,6)
        TNT1 A 0 A_CustomMissile("WizardSpell",32,0,-6)
        WZRD F 4 Bright
        WZRD AAABBB 2 A_Wander
        Goto See

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
    Pain.KubiLungeAttackSheathe:
    Pain.KubiSpinAttackSheathe:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain
    Pain.KubiRangeAttack:
    Pain.KubiRangeAttackSheathe:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain
    Death.KubiRangeAttack:
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
    Death.KubiRangeAttackSheathe:
    Death.KubiLungeAttackSheathe:
    Death.KubiSpinAttackSheathe:
    Death.Knife:
    Death.Triumph:
    //Death.OmenHeavyMetalThunder:
        TNT1 A 0 A_GiveToTarget("Points",4500)
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        //TNT1 A 0 A_GiveToTarget("SuperMeterCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_PlaySound("enemy/bisected")
        goto death//.kubiuti+2
    Pain.FrosthammerCharged:
    Pain.FrosthammerExplode:
    Pain.Frosthammer:
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("IceFog2",random(-1,1),random(-1,1),random(16,46),random(-500,500)/100.00,random(-500,500)/100.00,random(-200,500)/100.00,random(-180,180),161)
        TNT1 A 0 ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_SLOWDOWN,0,0)
    Pain.SabbathDamage:
    Pain.KharonSheathe:
        WZRD C 5 //A_Recoil(1)
        WZRD C 5 A_Pain
        Goto See
    Pain:
        WZRD C 3
        WZRD C 3 A_Pain
        Goto See

    Pain.Taser:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        WZRD C 2 BRIGHT A_PlaySoundEx("taser/sparks","SoundSlot5",0)
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT A_PlaySoundEx("taser/sparks","SoundSlot5",0)
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        WZRD C 2 BRIGHT
        WZRD C 2
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        Goto See


    Death.Kubiuti:
    Death.KubiutiUpper:
    Death.KubiutiCleave:
        TNT1 A 0 A_GiveToTarget("VirginguyComboPoints",1)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("enemy/bisected")
        TNT1 AA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",2)
        goto Death
    Death.HaltHammerzeitImpact:
    Death.HaltHammerzeit:
    Death.IronFistExplosion:
    Death.AcaciaDamage:
    Death.TestamentDamage:
    Death.SabbathDamage:
    Death.ExodusDamage:
    Death.KharonSheathe:
    Death.IronMaidenShot:
        TNT1 A 0 A_GiveToTarget("Points",4500)
        TNT1 A 0 A_JumpIfInTargetInventory("IAmASkilledPersonWhoWantsOnlyMySwordToGiveSouls",1,"Death")
        WZRD C 1
        TNT1 A 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))
    Death:
        WZRD G 6 A_Gravity
        WZRD H 6 Bright A_Scream
        WZRD I 5 Bright A_NoBlocking
        WZRD J 5 Bright
        WZRD K 5 Bright
        WZRD LM 4 Bright
        TNT1 AA 0 A_SpawnItemEx("WizardPuff",0,0,40,random(-50,50)/100.00,random(-50,50)/100.00,random(25,100)/100.00,random(-180,180),160)
        WZRD N 4 Bright A_SpawnItemEx("WizardPuff",0,0,40,0,0,0.5,0,160)
        WZRD O 4 //A_QueueCorpse
        WZRD P -1
        Stop
    Death.IronFistDamage:
    Death.IronFistUpper:
    Death.IronFistOverkill:
        TNT1 A 0 A_GiveToTarget("VirginguyComboPoints",1)
        TNT1 A 0 A_AlertMonsters
        TNT1 AA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",2)
        goto Death
    Death.UltimaBeam:
        TNT1 A 0 A_GiveToTarget("Points",4500)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_Scream
        WZRD CCCCCCCC 2 A_FadeOut(0.1)
        TNT1 A 0 A_NoBlocking
        NONE A 1
        stop
    Death.FrosthammerCharged:
    Death.FrosthammerExplode:
    Death.Frosthammer:
        TNT1 A 0 A_GiveToTarget("Points",4500)
        WZRD C 1
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("misc/freeze")
        TNT1 A 0 A_PlaySoundEx("frost/freeze","soundslot6")
        TNT1 A 0 A_SpawnItemEx("WizardguyFrozen",0,0,0,0,0,0,0,32)
        NONE A 5
        Stop

    Pain.LittleMacUppercut:
        TNT1 A 0 A_Recoil(5)
    Pain.ShihongSpinAttack:
        TNT1 A 0 A_PlaySoundEx("specialpunch/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("SpecialPunchContactQuake",1)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain
    Death.LittleMacUppercut:
        TNT1 A 0 A_PlaySoundEx("specialpunch/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("SpecialPunchContactQuake",1)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 ThrustThingZ(0,random(60,100),0,0)
        TNT1 A 0 ThrustThing(random(0,360),random(3,6),0,0)
        goto Death
    Death.ShihongSpinAttack:
        TNT1 A 0 A_PlaySoundEx("specialpunch/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("SpecialPunchContactQuake",1)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Death
    }
}

actor WizardguyFrozen : FrozenDummy
{
    +NOGRAVITY
    Radius 22
    States
    {
    Spawn:
        WZFR A 2100
        WZFR A 1 A_Jump(128, "Death")
        Loop
    }
}

ACTOR WizardPuff
{
    +NOBLOCKMAP
    +NOGRAVITY
    RenderStyle Translucent
    Alpha 0.95
    States
    {
    Spawn:
        WZRD QRST 6 Bright A_FadeOut(0.1)
        WZRD UV 7 Bright A_FadeOut(0.1)
        WZRD W 6 Bright A_FadeOut(0.1)
        Stop
    }
}

actor WizardSpell
{
    Radius 6
    Height 8
    Speed 10
    FastSpeed 20
    Damage (30)
    RenderStyle Add
    Alpha 1
    Scale 0.75
    DeathSound "wizard/shothit"
    Projectile
    +THRUGHOST
    States
    {
    Spawn:
        SPZR A 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        SPZR B 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        SPZR C 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        SPZR D 1 BRIGHT A_CustomMissile("WizardTrail1",0,0,0)
        Loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("EnemyCacoInfight",0,0,0,0,0,0,0,32)
        TNT1 AAAAAA 0 A_SpawnItemEx("WizardTrail2",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,0,160)
        SPZR E 1 BRIGHT A_Scream
        SPZR FGHIJKLMNOP 1 BRIGHT
        stop
    }
}

actor WizardTrail1
{
    Alpha 0.95
    Scale 0.55
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER)
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) <= 0,"Toaster0")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) == 1,"Toaster1")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) >= 2,"Toaster2")
        TNT1 A 1
        Loop
    Toaster0:
        TNT1 A 0 A_SpawnItemEx("WizardTrail2",0,0,0,random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        SPZR Q 1 BRIGHT A_FadeOut(0.1)
        SPZR Q 1 BRIGHT A_FadeOut(0.1)
        SPZR R 1 BRIGHT A_FadeOut(0.1)
        SPZR R 1 BRIGHT A_FadeOut(0.1)
        Loop
    Toaster1:
        SPZR QQRR 1 BRIGHT A_FadeOut(0.1)
        Loop
    Toaster2:
        TNT1 A 0
        Stop
   }
}

actor WizardTrail2 : WizardTrail1 
{
    Scale 0.25
    Alpha 0.85
    States
    {
    Spawn:
        SPZR QQRR 1 BRIGHT A_FadeOut(0.1)
        Loop
   }
}

ACTOR DeadCacoReplacement replaces DeadCacodemon
{
    Scale 0.8
    -SOLID
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        SKPL A -1
        stop
    }
}