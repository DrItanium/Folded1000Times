ACTOR WeeabooMastermind : SpiderMastermind replaces SpiderMastermind
{
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    +DONTRIP
    States
    {
    Missile:
        SPID A 10 Bright A_FaceTarget
        TNT1 A 0 A_PlaySound("sergeant/fire", CHAN_WEAPON)
        TNT1 A 0 A_CustomMissile("EnemyBullet",32,0,random(-750,-450)/100.00)
        TNT1 A 0 A_CustomMissile("EnemyBullet",32,0,random(450,750)/100.00)
        TNT1 AA 0 A_CustomMissile("EnemyBullet",32,0,random(-250,250)/100.00)
        SPID G 4 Bright
        SPID H 4
        TNT1 A 0 Bright A_SpidRefire
        Goto Missile+1

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        goto Super::Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",5)
        goto Super::Death
    Death.Kubiuti:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",10)
        goto Super::Death
    }
}

actor WeeabooCyberdemon : Cyberdemon replaces Cyberdemon
{
    Scale 1.1
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    +DONTRIP
    States
    {
    Missile:
        CYBR E 6 A_FaceTarget
        CYBR F 12 A_CustomMissile("CyberRocket",46,-18,0,0,0)
        CYBR E 12 A_FaceTarget
        CYBR F 12 A_CustomMissile("CyberRocket",46,-18,0,0,0)
        CYBR E 12 A_FaceTarget
        CYBR F 12 A_CustomMissile("CyberRocket",46,-18,0,0,0)
        Goto See
    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        goto Super::Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",5)
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        goto Super::Death
    Death.Kubiuti:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",10)
        goto Super::Death
    }
}

actor CyberRocket : Rocket replaces Rocket
{
    Health 5
    Species "EnemyShot"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "ExodusJump", 0.0
    +THRUSPECIES
    +SHOOTABLE
    +THRUGHOST
    -NOBLOCKMAP 
    +NOBLOOD
    -FIREDAMAGE
    States
    {
    Death:
        TNT1 A 0 A_UnsetShootable
        goto Super::Death
    Death.Kubiuti: // Doesn't work, for some reason.
    Death.GuardMove:
        TNT1 A 0 A_UnsetShootable
        MISL B 8 Bright
        MISL C 6 Bright
        MISL D 4 Bright
        Stop
    }
}