// Swan Fox belongs to Shiga95/Angel_Neko-X, generous enough to allow me to use
// her in a cameo. Thank you very much!

ACTOR SwanFoxDakimakura replaces CommanderKeen
{
    Health 200
    Radius 16
    Height 72
    Mass 0x7FFFFFFF
    Scale 0.45
    PainChance 256
    +SOLID
    +SHOOTABLE
    +COUNTKILL
    +NOICEDEATH
    +ISMONSTER
    +NOBLOOD
    //BloodType "HammerSmoke"
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    Species "Body"
    PainSound "daki/pain"
    DeathSound "daki/death"
    States
    {
    Spawn:
        DAKI A -1
        Loop
    Death:
        TNT1 AAAAAA 0 A_SpawnItemEx("BulletSmoke",0,0,random(16,32),random(-75,75)/100.00,random(-75,75)/100.00,random(-75,75)/100.00,0,160)
        TNT1 AAAAAA 0 A_SpawnItemEx("DakiSmoke",0,0,random(16,32),random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        TNT1 A 0 A_PlaySound("daki/pomf")
        DAKI AB 3
        DAKI C 3 A_Scream
        DAKI DE 2
        DAKI F 2
        DAKI G 2 A_NoBlocking
        DAKI H 2 A_KeenDie
        DAKI H -1
        Stop
    Pain:
        TNT1 AAAAAA 0 A_SpawnItemEx("BulletSmoke",0,0,random(16,32),random(-75,75)/100.00,random(-75,75)/100.00,random(-75,75)/100.00,0,160)
        TNT1 AAAAAA 0 A_SpawnItemEx("DakiSmoke",0,0,random(16,32),random(-150,150)/100.00,random(-150,150)/100.00,random(-150,150)/100.00,0,160)
        TNT1 A 0 A_PlaySound("daki/pomf")
        DAKI I 1
        DAKI I 4 A_Pain
        Goto Spawn
    }
}

actor DakiSmoke
{
    RenderStyle Translucent
    Alpha 0.4
    Scale 0.4
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        FHFX STUVW 2
        stop
    }
}