actor WeeabooRevenant : Revenant replaces Revenant
{
    +MISSILEMORE
    +MISSILEEVENMORE
    +ALWAYSFAST // HAVE FUN, FUCKERS.
    -LONGMELEERANGE
    Species "Body"
    PainChance "kubiuti", 255
    PainChance "IronFistDamage", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    States
    {
    Melee:
    Missile:
        SKEL K 5 A_FaceTarget
        TNT1 A 0 A_FaceTarget
        SKEL J 5 BRIGHT A_CustomMissile("RevenantTracer",40,12,0,0)//A_SkelMissile
        SKEL J 10
        SKEL K 5 A_FaceTarget
        TNT1 A 0 A_FaceTarget
        SKEL J 5 A_CustomMissile("RevenantTracer",40,-12,0,0)//A_SkelMissile
        SKEL J 10
        Goto See

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        goto Super::Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        //TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto death.kubiuti+2

    Death.IronFistDamage:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",4)
    death.Bloody:
        SKEL RRR 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL S 0 A_Scream
        SKEL SS 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL T 0 A_NoBlocking
        SKEL TT 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL UUVVWWXX 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL YYYYY 2 A_SpawnDebris("BloodShower2",0)
        SKEL Y -1
        Stop   
    death.kubiuti:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",4)
        XDF1 A 0 A_PlaySound("enemy/bisected")
        XDF1 A 0 A_Fall
        XDF1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(32,48),0,random(0,360),2,random(0,360))
        XDF1 A 0 A_Jump(128,"kubiuti2","death.Bloody")
        XDF1 A 0 A_SpawnItem("SKEXKubiuted",0,40,0)
        XDF1 AAAAAAAAAA 2 A_ThrowGrenade ("BloodSplashGus1",-2,5+random(0,7),40)    
        XDF1 B 0 A_NoBlocking
        XDF1 BBBCCC 2 A_ThrowGrenade ("BloodSplashGus1",-2,5+random(0,7),40)    
        XDF1 DDDDD 2 A_SpawnDebris("BloodShower2",0)
        XDF1 D -1
        Stop
    kubiuti2:
        XDF2 A 0 A_SpawnItem("SKEXKubiuted",0,40,0)
        XDF2 A 0 A_Recoil(-1)
        XDF2 A 0 A_Wander
        XDF2 AA 3 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDF2 B 0 A_Recoil(-1)
        XDF2 B 0 A_Wander
        XDF2 BB 3 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDF2 B 0 A_Jump(32,1)
        goto kubiuti2+1
        XDF2 CDEF 4 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDF2 G -1
        Stop
    }
}

actor RevenantTracer2 : RevenantTracer replaces RevenantTracer
{
    Health 5
    Species "EnemyShot"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "EnemyInfightDamageCyber", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "ExodusDamage", 0.0
    DamageFactor "Kubiuti", 0.0
    DamageFactor "IronFistDamage", 0.0
    DamageFactor "HaltHammerzeitImpact", 0.0
    +THRUSPECIES
    +SHOOTABLE
    +THRUGHOST
    -NOBLOCKMAP 
    +NOBLOOD
    States
    {
    Death:
        TNT1 A 0 A_UnsetShootable
        TNT1 A 0 A_SpawnItemEx("EnemyRevenantInfight",0,0,0,0,0,0,0,32)
        goto Super::Death
    }
}

actor SKEXKubiuted : POSXKubiuted
{
    States
    {
    Spawn:
        XDF1 E 0
        XDF1 E 0 ThrustThing (random(0,255),random(1,4))
        XDF1 E 0 ThrustThingZ(0,random(15,40),0,0)
        XDF1 EEEEFFFFG 1 A_SpawnItem("BloodShower3",0)
        XDF1 G 0 A_CheckFloor("Spawned")
        goto Spawn+11
    Spawned:
        XDF1 H 0 A_PlaySound("S_Blodd")
        XDF1 H 0 A_ChangeFlag("PUSHABLE",0)
    Spawned2:    
        XDF1 H 3 A_Look
        goto Spawned2
    See:    
        XDF1 H 3 A_Chase
        loop
    Melee:
        XDF1 H 1 A_FaceTarget
        XDF1 H 0 damageThing(1)
        XDF1 H 0 A_ChangeFlag("PUSHABLE",1)
        XDF1 H 0 ThrustThingZ(0,random(15,40),0,0)
        XDF1 H 0 A_Recoil(2+random(0,8))
        goto Spawn+3
    Death:
        BLD1 C 0 A_Fall
        BLD1 C 0 A_Scream
        BLD1 CCCCC 0 A_SpawnDebris("BloodShower2")
        BLD1 CBA 1
        Stop
    }
}