actor WeeabooRevenant : Revenant replaces Revenant
{
    +MISSILEMORE
    +MISSILEEVENMORE
    +ALWAYSFAST // HAVE FUN, FUCKERS.
    -LONGMELEERANGE
    Species "Body"
    PainChance "kubiuti", 255
    PainChance "KubiSpinAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    States
    {
    Missile:
        SKEL J 10 Bright A_FaceTarget
        SKEL K 5 A_CustomMissile("RevenantTracer",40,12,0,0)//A_SkelMissile
        SKEL K 5 A_FaceTarget
        SKEL J 1 Bright A_FaceTarget
        SKEL J 4 Bright A_FaceTarget
        SKEL K 5 A_CustomMissile("RevenantTracer",40,-12,0,0)//A_SkelMissile
        SKEL K 5 A_FaceTarget
        Goto See

    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        goto Super::Pain
    Death.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
        goto death.kubiuti+1

    death.Bloody:
        SKEL RRR 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL S 0 A_Scream
        SKEL SS 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL T 0 A_NoBlocking
        SKEL TT 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL UUVVWWXX 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL YYYYY 2 A_SpawnDebris("BloodShower2",0)
        SKEL Y -1
        Stop   
    death.kubiuti:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",4)
        XDF1 A 0 A_PlaySound("enemy/bisected")
        XDF1 A 0 A_Fall
        XDF1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(32,48),0,random(0,360),2,random(0,360))
        XDF1 A 0 A_Jump(128,"kubiuti2","death.Bloody")
        XDF1 A 0 A_SpawnItem("SKEXKubiuted",0,40,0)
        XDF1 AAAAAAAAAA 2 A_ThrowGrenade ("BloodSplashGus1",-2,5+random(0,7),40)    
        XDF1 B 0 A_NoBlocking
        XDF1 BBBCCC 2 A_ThrowGrenade ("BloodSplashGus1",-2,5+random(0,7),40)    
        XDF1 DDDDD 2 A_SpawnDebris("BloodShower2",0)
        XDF1 D -1
        Stop
    kubiuti2:
        XDF2 A 0 A_SpawnItem("SKEXKubiuted",0,40,0)
        XDF2 A 0 A_Recoil(-1)
        XDF2 A 0 A_Wander
        XDF2 AA 3 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDF2 B 0 A_Recoil(-1)
        XDF2 B 0 A_Wander
        XDF2 BB 3 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDF2 B 0 A_Jump(32,1)
        goto kubiuti2+1
        XDF2 CDEF 4 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDF2 G -1
        Stop
    }
}

actor RevenantTracer2 : RevenantTracer replaces RevenantTracer
{
    Health 5
    Species "EnemyShot"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "ExodusJump", 0.0
    +THRUSPECIES
    +SHOOTABLE
    +THRUGHOST
    -NOBLOCKMAP 
    +NOBLOOD
    States
    {
    Death:
        TNT1 A 0 A_UnsetShootable
        goto Super::Death
    }
}

actor SKEXKubiuted : POSXKubiuted
{
    States
    {
    Spawn:
        XDF1 E 0
        XDF1 E 0 ThrustThing (random(0,255),random(1,4))
        XDF1 E 0 ThrustThingZ(0,random(15,40),0,0)
        XDF1 EEEEFFFFG 1 A_SpawnItem("BloodShower3",0)
        XDF1 G 0 A_CheckFloor("Spawned")
        goto Spawn+11
    Spawned:
        XDF1 H 0 A_PlaySound("S_Blodd")
        XDF1 H 0 A_ChangeFlag("PUSHABLE",0)
    Spawned2:    
        XDF1 H 1 A_chase
        goto Spawned2
    Melee:
        XDF1 H 1 A_FaceTarget
        XDF1 H 0 damageThing(1)
        XDF1 H 0 A_ChangeFlag("PUSHABLE",1)
        XDF1 H 0 ThrustThingZ(0,random(15,40),0,0)
        XDF1 H 0 A_Recoil(2+random(0,8))
        goto Spawn+3
    Death:
        BLD1 C 0 A_Fall
        BLD1 C 0 A_Scream
        BLD1 CCCCC 0 A_SpawnDebris("BloodShower2")
        BLD1 CBA 1
        Stop
    }
}