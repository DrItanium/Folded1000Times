ACTOR Priest replaces Arachnotron
{
    Health 500
    Speed 12
    Radius 24
    Height 56
    Gravity 0.75
    Mass 600
    PainChance 192
    PainChance "kubiuti", 255
    PainChance "IronFistDamage", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "KubiRangeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    Species "Body"
    Monster
    +FLOORCLIP
    +MISSILEMORE
    +MISSILEEVENMORE
    +BOSSDEATH
    SeeSound "priest/see"
    PainSound "priest/pain"
    DeathSound "priest/death"
    ActiveSound "priest/idle"
    Obituary "%o was desecrated by a Priest."
    States
    {
    Spawn:
        PRST A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_ChangeFlag("PAINLESS",0)
        PRST AA 2 A_Chase
        PRST BB 2 A_FastChase
        PRST CC 2 A_Chase
        PRST DD 2 A_FastChase
        Loop
    Missile:
        TNT1 A 0 A_Jump(32,"Missile3")
        TNT1 A 0 A_Jump(64,"Missile2")
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        PRST ABCD 2 A_Wander
        TNT1 A 0 A_Jump(128,"See")
        TNT1 A 0 A_CPosRefire
        goto Missile
    Missile2:
        TNT1 A 0 A_Jump(256,"StrafeLeft","StrafeRight")
    StrafeLeft:
        TNT1 A 0 ThrustThingZ(0,random(20,30),0,1)
	    TNT1 A 0 ThrustThing(angle*256/360+192,random(16,24),0,0)
	    goto Missile2B
    StrafeRight:
        TNT1 A 0 ThrustThingZ(0,random(20,30),0,1)
	    TNT1 A 0 ThrustThing(angle*256/360+64,random(16,24),0,0)
    Missile2B:
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        goto See
    Missile3:
        TNT1 A 0 ThrustThingZ(0,random(50,70),0,1)
        TNT1 A 0 A_Recoil(random(4,8))
        TNT1 A 0 A_ChangeFlag("PAINLESS",1)
    Missile3B:
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        TNT1 A 0 A_CheckFloor("See")
        goto Missile3B
    Pain2:
        TNT1 A 0
        TNT1 A 0 ThrustThingZ(0,random(15,30),0,1)
        TNT1 A 0 A_Recoil(random(4,8))
        PRST E 2 A_Pain
        Goto See
    Pain.Kubiuti:
    Pain:
        PRST E 3 A_Stop
        PRST E 3 A_Pain
        Goto See

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain2
    Pain.KubiRangeAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
    Death.KubiRangeAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        //TNT1 A 0 A_GiveToTarget("SuperMeterCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Death

    Death:
        TNT1 A 0
        PRST H 4 A_Scream
        PRST I 4
        PRST J 4 A_NoBlocking
        PRST KLMN 4
        PRST O -1
        stop
    Death.IronFistDamage:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",3)
        TNT1 A 0
        TNT1 AAAAAAAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(0,32),0,random(0,360),2,random(0,360))
        PRST P 2 A_XScream
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(32,48),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        PRST QQ 1 A_SpawnDebris("BloodShower2",1)
        TNT1 A 0 A_NoBlocking
        PRST RRRSSSTTTUUUVVV 1 A_SpawnDebris("BloodShower2",1)
        PRST W -1
        stop
    }
}

ACTOR PriestFire
{
    Radius 13
    Height 8
    Health 5
    Speed 20
    Damage 5
    Projectile
    +RANDOMIZE
    RenderStyle Add 
    Alpha 0.95
    Scale 0.9
    DeathSound "priest/shothit"
    Species "EnemyShot"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "EnemyInfightDamageImp", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "EnemyInfightDamageNoble", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "EnemyInfightDamageFatso", 0.0
    DamageFactor "EnemyInfightDamageCaco", 0.0
    DamageFactor "EnemyInfightDamageCyber", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "ExodusJump", 0.0
    DamageFactor "ExodusDamage", 0.0
    DamageFactor "Kubiuti", 0.0
    DamageFactor "IronFistExplode", 0.0
    DamageFactor "HaltHammerzeitImpact", 0.0
    +THRUSPECIES
    +SHOOTABLE
    +THRUGHOST
    -NOBLOCKMAP 
    +NOBLOOD
    States
    {
    Spawn:
        TNT1 AA 0 A_SpawnItemEx("PriestFireTrail",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-150,150)/80.00,random(-150,150)/80.00,random(-150,150)/80.00,0,160)
        DMFX A 1 Bright A_SpawnItemEx("PriestFireTrail",0,0,0,0,0,0,0,160)
        TNT1 AA 0 A_SpawnItemEx("PriestFireTrail",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-150,150)/80.00,random(-150,150)/80.00,random(-150,150)/80.00,0,160)
        DMFX B 1 Bright A_SpawnItemEx("PriestFireTrail",0,0,0,0,0,0,0,160)
        TNT1 AA 0 A_SpawnItemEx("PriestFireTrail",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-150,150)/80.00,random(-150,150)/80.00,random(-150,150)/80.00,0,160)
        DMFX C 1 Bright A_SpawnItemEx("PriestFireTrail",0,0,0,0,0,0,0,160)
        Loop
    Death:
        TNT1 A 0 A_UnsetShootable
        TNT1 A 0 A_SpawnItemEx("EnemyArachnotronInfight",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_Scream
        DMFX EFGH 2 Bright
        Stop
    }
}

actor PriestFireTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Alpha 0.75
    Scale 0.65
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 2
    Spawn2:
        DMFX A 1 BRIGHT A_FadeOut(0.125)
        DMFX B 1 BRIGHT A_FadeOut(0.125)
        DMFX C 1 BRIGHT A_FadeOut(0.125)
        loop
    }
}