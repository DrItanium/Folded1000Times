actor WeeabooArachnotron : Arachnotron replaces Arachnotron
{
    Species "Body"
    PainChance "kubiuti", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    States
    {
    Missile:
        BSPI A 25 A_FaceTarget
        BSPI G 4 Bright A_CustomMissile("ArachnotronPlasma",16,0,0)
        BSPI H 4
        BSPI H 1 A_SpidRefire
        Goto Missile+1

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        goto Super::Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto death.kubiuti+1
  
    death.rocket:
        BSPI J 0 A_CustomMissile("BloodShower2",40,0,45)
        BSPI J 0 A_CustomMissile("BloodShower2",40,0,-45)
        BSPI J 0 A_CustomMissile("BloodShower2",40,0,130)
        BSPI J 0 A_CustomMissile("BloodShower2",40,0,-130)
        BSPI J 5 A_NoBlocking
        XDH1 AAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        XDH1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        XDH1 AAA 0 A_SpawnItem("BSPIKubiuted",0)
        XDH1 A 0 A_SpawnItem("BSPIKubiuted2",0)
        XDH1 A 1 A_XScream
        XDH1 AAAAA 0 A_CustomMissile("BloodShower5",random(32,48),0,random(0,360),2,random(0,360))
        XDH1 AAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        XDH1 AAAAAA 1 A_SpawnDebris("BloodShower2",1)
        XDH1 AAAAA 0 A_CustomMissile("BloodShower5",random(32,48),0,random(0,360),2,random(0,360))
        XDH1 BBBBBBCCCCCCDDDD 1 A_SpawnDebris("BloodShower2",1)
        XDH1 D -1 A_BossDeath
        stop
    death.Bloody:
        BSPI J 0 A_Scream
        BSPI JJJJJJJJJJ 2 A_ThrowGrenade("BloodShower2",-2,5+random(0,7),5+random(0,7))
        BSPI K 0 A_NoBlocking
        BSPI KKKKKKK 1 A_ThrowGrenade("BloodShower2",-2,5+random(0,7),5+random(0,7))
        BSPI L 7
        BSPI MNO 7
        BSPI P -1 A_BossDeath
        stop
    death.kubiuti:
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",3)
        TNT1 A 0 A_Jump(196,"Death.Rocket","Death.Bloody")
        BSPI J 20 A_PlaySound("enemy/bisected")
        goto death+1
    }
}

actor ArachnotronPlasma2 : ArachnotronPlasma replaces ArachnotronPlasma
{
    Health 5
    Speed 30
    Species "EnemyShot"
    DamageFactor "EnemyInfightDamage", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "ExodusJump", 0.0
    +THRUSPECIES
    +SHOOTABLE
    +THRUGHOST
    -NOBLOCKMAP 
    +NOBLOOD
    States
    {
    Death:
        TNT1 A 0 A_UnsetShootable
        goto Super::Death
    }
}



actor BSPIKubiuted : POSXKubiuted
{
    Health    1
    States
    {
    Spawn:
        XDH1 E 0
        XDH1 E 0 ThrustThing (random(0,255),random(1,4))
        XDH1 E 0 ThrustThingZ(0,random(15,40),0,0)
        XDH1 EEEEFFFFGGGGHHHH 1 A_SpawnItem("BloodShower3",0)
        XDH1 H 0 A_CheckFloor("Spawned")
        goto Spawn+3
    Spawned:
        XDH1 I 0 A_PlaySound("S_Blodd")
        XDH1 I 0 A_ChangeFlag("PUSHABLE",0)
    Spawned2:
        XDH1 I 0 A_Jump(128,2)
        XDH1 I 1 A_Chase
        goto Spawned2+1
        XDH1 J 1 A_Chase
        goto Spawned2+2

    Melee:
        XDH1 J 1 A_FaceTarget
        XDH1 J 0 damageThing(1)
        XDH1 J 0 A_ChangeFlag("PUSHABLE",1)
        XDH1 J 0 ThrustThingZ(0,random(15,40),0,0)
        XDH1 J 0 A_Recoil(2+random(0,8))
        goto Spawn+3
    Death:
        BLD1 C 0 A_Fall
        BLD1 C 0 A_Scream
        BLD1 CCCCC 0 A_SpawnDebris("BloodShower2")
        BLD1 CBA 1
        Stop
    }
}

actor BSPIKubiuted2 : POSXKubiuted
{
    States
    {
    Spawn:
        XDH2 A 0
        XDH2 A 0 ThrustThing (random(0,255),random(1,4))
        XDH2 A 0 ThrustThingZ(0,random(15,40),0,0)
        XDH2 AABBCCDDEEFFGGHH 1 A_SpawnItem("BloodShower3",0,24)
        XDH2 A 0 A_CheckFloor("Spawned")
        goto Spawn+3
    Spawned:
        XDH2 I 0 A_PlaySound("S_Blodd")
        XDH2 I 0 A_ChangeFlag("PUSHABLE",0)
    Spawned2:
        XDH2 I 0 A_Jump(128,2)
        XDH2 I 1 A_Chase
        goto Spawned2+1
        XDH2 J 1 A_Chase
        goto Spawned2+2

    Melee:
        XDH2 A 1 A_FaceTarget
        XDH2 A 0 damageThing(1)
        XDH2 A 0 A_ChangeFlag("PUSHABLE",1)
        XDH2 A 0 ThrustThingZ(0,random(15,40),0,0)
        XDH2 A 0 A_Recoil(2+random(0,8))
        goto Spawn+3
    Death:
        BLD1 C 0 A_Fall
        BLD1 C 0 A_Scream
        BLD1 CCCCC 0 A_SpawnDebris("BloodShower2")
        BLD1 CBA 1
        Stop
    }
}